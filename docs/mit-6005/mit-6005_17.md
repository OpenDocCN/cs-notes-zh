# 第 17 篇阅读：正则表达式和语法

#### 6.005 中的软件

| 免受错误 | 易于理解 | 准备好变化 |
| --- | --- | --- |
| 今天正确且未来未知也正确。 | 与未来的程序员清晰沟通，包括未来的你。 | 设计以适应变化而无需重写。 |

#### 目标

今天的课后，你应该：

+   理解语法产生和正则表达式运算符的思想

+   能够阅读语法或正则表达式，并确定它是否匹配一个字符序列

+   能够编写语法或正则表达式来匹配一组字符序列并将其解析成数据结构

## 介绍

今天的阅读介绍了几个思想：

+   具有产生物、非终端、终端和操作符的语法

+   正则表达式

+   解析器生成器

一些程序模块以字节序列或字符序列的形式接收输入或产生输出，当它被简单地存储在内存中时，被称为*字符串*，当它流入或流出模块时，被称为*流*。在今天的阅读中，我们讨论了如何为这样的序列编写规范。具体来说，一系列字节或字符可能是：

+   磁盘上的文件，此时规范称为*文件格式*

+   通过网络发送的消息，此时规范是*线协议*

+   用户在控制台上键入的命令，此时规范是*命令行界面*

+   存储在内存中的字符串

对于这些类型的序列，我们引入了*语法*的概念，它不仅允许我们区分合法和非法的序列，还允许我们将一个序列解析成程序可以处理的数据结构。从语法产生的数据结构通常是像我们在递归数据类型阅读中讨论过的那样的递归数据类型。

我们还讨论了一种称为*正则表达式*的语法的专门形式。除了用于规范和解析之外，正则表达式还是一种广泛使用的工具，用于许多需要拆解字符串、从中提取信息或转换它的字符串处理任务。

下面的阅读将讨论解析器生成器，一种工具，可以将语法自动转换为该语法的解析器。

## 语法

为了描述一系列符号，无论它们是字节、字符还是从固定集合中提取的其他类型的符号，我们使用了一个称为*语法*的紧凑表示。

*语法*定义了一组句子，其中每个*句子*都是符号序列。例如，我们的 URL 语法将指定 HTTP 协议中的合法 URL 集。

句子中的符号称为*终端*（或标记）。

它们被称为终端，因为它们是代表句子结构的树的叶子。它们没有任何子节点，也不能进一步扩展。我们通常用引号写终端，比如`'http'`或`':'`。

语法由一组*产生式*描述，其中每个产生式定义一个*非终结符*。你可以把一个非终结符看作是代表一组句子的变量，而产生式则是对这个变量的定义，以其他变量（非终结符）、运算符和常量（终结符）为基础。非终结符是表示句子的树的内部节点。

语法中的一个产生式的形式是

> 非终结符 ::= 终结符、非终结符和运算符的表达式

在 6.005 中，我们将使用小写标识符来命名非终结符，如`x`或`y`或`url`。

语法中的一个非终结符被指定为*根*。语法识别的句子集合是与根非终结符匹配的句子。这个非终结符通常被称为`root`或`start`，但在下面的语法中，我们通常会选择更容易记忆的名称，如`url`、`html`和`markdown`。

### 语法运算符

产生式表达式中的三个最重要的运算符是：

+   连接

```
x ::= y z     an x is a y followed by a z 
```

+   重复

```
x ::= y*      an x is zero or more y 
```

+   联合（也称为交替）

```
x ::= y | z     an x is a y or a z 
```

你还可以使用其他仅是语法糖的附加运算符（即，它们等价于大三运算符的组合）：

+   选项（0 或 1 次出现）

```
x ::=  y?      an x is a y or is the empty sentence
```

+   1+ 重复（1 次或更多次）

```
x ::= y+       an x is one or more y
               (equivalent to  x ::= y y* )
```

+   字符类

```
x ::= [abc]  is equivalent to  x ::= 'a' | 'b' | 'c' 

x ::= [^b]   is equivalent to  x ::= 'a' | 'c' | 'd' | 'e' | 'f' 
                                         | ... (all other characters)
```

按照惯例，运算符`*`、`?`和`+`具有最高的优先级，这意味着它们首先被应用。交替`|`具有最低的优先级，这意味着它在最后应用。括号可以用于覆盖这个优先级，使得一个序列或交替可以重复：

+   使用括号进行分组

```
x ::=  (y z | a b)*   an x is zero or more y-z or a-b pairs
```

#### 阅读练习

阅读语法 1

考虑这个语法：

```
S ::= (B C)* T
B ::= M+ | P B P
C ::= B | E+
```

（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

阅读语法 2

```
root ::= 'a'+ 'b'* 'c'?
```

（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

阅读语法 3

```
root    ::= integer ('-' integer)+
integer ::= [0-9]+
```

（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

阅读语法 4

```
root   ::= (A B)+
A      ::= [Aa]
B      ::= [Bb]
```

（缺失答案）（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

### 例如：网址

假设我们想要编写一个代表网址的语法。让我们通过从简单的示例开始并随着扩展语法而渐进地构建语法。

这是一个简单的网址：

```
http://mit.edu/
```

代表*只有这个网址*的句子集合的语法如下：

```
url ::= 'http://mit.edu/'
```

但让我们将其泛化以捕获其他领域：

```
http://stanford.edu/
http://google.com/
```

我们可以将其写成一行，如下所示：

```
url ::= 'http://' [a-z]+ '.' [a-z]+  '/'
```

此语法表示仅由两部分主机名组成的所有 URL，其中主机名的每个部分由 1 个或多个字母组成。因此，`http://mit.edu/`和`http://yahoo.com/`将匹配，但`http://ou812.com/`不会。由于它只有一个非终结符，这个 URL 语法的*解析树*将看起来像右侧的图片。

在这种单行形式中，具有仅使用运算符和终结符的产生式的单一非终结符的语法称为*正则表达式*（稍后详细介绍）。但如果我们使用新的非终结符命名部分，将更容易理解：

![使用具有 url、主机名和单词非终结符的语法解析'http://mit.edu'生成的解析树](http://web.mit.edu/6.005/www/fa16/classes/17-regex-grammarsfigures/url-with-hostname.png)

```
url ::= 'http://' hostname '/'
hostname ::= word '.' word
word ::= [a-z]+
```

此语法的解析树现在显示在右侧。树现在具有更多结构。树的叶子是已解析的字符串部分。如果我们将叶子连接在一起，我们将恢复原始字符串。`主机名`和`单词`非终结符标记了树的节点，其子树与语法中的规则匹配。请注意，像`主机名`这样的非终结符节点的直接子节点遵循`主机名`规则，`单词'.'单词`。

我们还需要进行哪些泛化？主机名可以有多个组件，还可以有一个可选的端口号：

```
http://didit.csail.mit.edu:4949/
```

为了处理这种类型的字符串，现在的语法是：

![使用具有递归主机名规则的语法解析'http://mit.edu'生成的解析树](http://web.mit.edu/6.005/www/fa16/classes/17-regex-grammarsfigures/url-with-recursive-hostname.png)

```
url ::= 'http://' hostname (':' port)? '/' 
hostname ::= word '.' hostname | word '.' word
port ::= [0-9]+
word ::= [a-z]+
```

*请注意，主机名现在是递归地定义的。* 主机名定义的哪一部分是基本情况，哪一部分是递归步骤？允许哪些类型的主机名？

使用重复运算符，我们也可以这样写主机名：

```
hostname ::= (word '.')+ word
```

另一个需要观察的是，此语法允许端口号不符合技术上的规定，因为端口号只能范围从 0 到 65535。我们可以编写更复杂的端口定义，只允许这些整数，但在语法中通常不这样做。相反，约束 0 <= 端口 <= 65535 将与语法一起指定。

还有更多我们应该做的事情以走得更远：

+   将`http`泛化以支持 URL 可能具有的其他协议

+   将结尾的`/`泛化为斜杠分隔的路径

+   允许使用完整合法字符集而不仅仅是 a-z 的主机名

#### 阅读练习

编写语法

假设我们希望`url`语法也匹配以下形式的字符串：

```
https://websis.mit.edu/
ftp://ftp.athena.mit.edu/
```

但不是以下形式的字符串：

```
ptth://web.mit.edu/
mailto:bitdiddle@mit.edu
```

因此，我们将语法更改为：

```
url ::= protocol '://' hostname (':' port)? '/' 
protocol ::= TODO
hostname ::= word '.' hostname | word '.' word
port ::= [0-9]+
word ::= [a-z]+
```

（缺失答案）（缺失答案）（缺失答案）（缺失答案）

（缺失解释）

### 示例：Markdown 和 HTML

现在让我们看看一些文件格式的语法。我们将使用两种不同的标记语言，它们表示文本中的排版样式。这里它们是：

#### Markdown

```
This is _italic_.
```

#### HTML

```
Here is an <i>italic</i> word.
```

为简单起见，我们的示例 HTML 和 Markdown 语法仅指定了斜体，但当然还有其他文本样式可能。

这是我们简化版 Markdown 的语法：

![由 Markdown 语法产生的解析树](http://web.mit.edu/6.005/www/fa16/classes/17-regex-grammarsfigures/markdown.png)

```
markdown ::=  ( normal | italic ) *
italic ::= '_' normal '_'
normal ::= text
text ::= [^_]*
```

这是我们简化版 HTML 的语法：

![由 HTML 语法产生的解析树](http://web.mit.edu/6.005/www/fa16/classes/17-regex-grammarsfigures/html.png)

```
html ::=  ( normal | italic ) *
italic ::= '<i>' html '</i>'
normal ::= text
text ::= [^<>]*
```

#### 阅读练习

递归语法

查看上面的`markdown`和`html`语法，并比较它们的`italic`产生式。注意，它们不仅在分隔符（在一个情况下为`_`，在另一个情况下为`< >`标记）上有所不同，而且在这些分隔符之间匹配的非终结符也不同。一个语法是递归的；另一个语法则不是。

对于下面的每个字符串，如果将指定的语法与之匹配，哪些字母在与`italic`非终结符匹配的内容内？您的答案应该是字母`abcde`的某个子集。

（缺少答案）

（缺少解释）

（缺少答案）

（缺少解释）

## 正则表达式

一个*普通*语法具有特殊属性：通过用其右侧替换每个非终结符（除了根终结符），您可以将其减少到根的单个产生式，右侧仅包含终结符和运算符。

我们的 URL 语法是正则的。通过用它们的产生式替换非终结符，它可以被减少为单个表达式：

```
url ::= 'http://' ([a-z]+ '.')+ [a-z]+ (':' [0-9]+)? '/' 
```

Markdown 语法也是正则的：

```
markdown ::= ([^_]* | '_' [^_]* '_' )*
```

但是我们的 HTML 语法无法完全简化。通过用非终结符的右侧替换，您最终可以将其减少到类似于这样的东西：

```
html ::=  ( [^<>]* | '<i>' html '</i>' )*
```

但是右侧的`html`的递归使用无法消除，并且也不能简单地用重复运算符替换。因此，HTML 语法不是正则的。

终端和运算符的简化表达式可以以更紧凑的形式编写，称为*正则表达式*。正则表达式摒弃了终端周围的引号和终端与运算符之间的空格，因此它只包含终端字符、用于分组的括号和运算符字符。例如，我们的`markdown`格式的正则表达式就是

```
([^_]*|_[^_]*_)*
```

正则表达式也被简称为*regexes*。正则表达式比原始语法不太可读，因为它缺少记录了每个子表达式含义的非终结符名称。但是正则表达式的实现速度很快，并且许多编程语言中都有支持正则表达式的库。

在编程语言库中常见的正则表达式语法还有一些特殊运算符，除了我们在语法中使用的运算符外。这里是一些常用的有用的运算符：

```
.       any single character

\d      any digit, same as [0-9]
\s      any whitespace character, including space, tab, newline
\w      any word character, including letters and digits

\., \(, \), \*, \+, ...
        escapes an operator or special character so that it matches literally
```

当存在会与特殊字符混淆的终端字符时，使用反斜杠是很重要的。因为我们的`url`正则表达式中包含`.`作为终端字符，所以我们需要使用反斜杠来转义它：

```
http://([a-z]+\.)+[a-z]+(:[0-9]+)/
```

#### 阅读练习

正则表达式

考虑以下正则表达式：

```
[A-G]+(♭|♯)?
```

缺失答案 缺失答案 缺失答案 缺失答案 缺失答案

缺失解释

### 在 Java 中使用正则表达式

正则表达式（"regexes"）在编程中被广泛使用，你应该将它们放入你的工具箱中。

在 Java 中，你可以使用正则表达式来操作字符串（参见[`String.split`](https://docs.oracle.com/javase/8/docs/api/java/lang/String.html#split-java.lang.String-)、[`String.matches`](https://docs.oracle.com/javase/8/docs/api/java/lang/String.html#matches-java.lang.String-)、[`java.util.regex.Pattern`](https://docs.oracle.com/javase/8/docs/api/java/util/regex/Pattern.html)）。它们作为现代脚本语言的一流特性内置在其中，如 Python、Ruby 和 Javascript，并且你可以在许多文本编辑器中使用它们进行查找和替换。正则表达式是你的朋友！大多数时候。以下是一些示例。

用单个空格替换所有连续的空格：

```
String singleSpacedString = string.replaceAll(" +", " ");
```

匹配一个 URL：

```
Pattern regex = Pattern.compile("http://([a-z]+\\.)+[a-z]+(:[0-9]+)?/");
Matcher m = regex.matcher(string);
if (m.matches()) {
    // then string is a url
}
```

提取 HTML 标签的一部分：

```
Pattern regex = Pattern.compile("<a href='\"['\"]>");
Matcher m = regex.matcher(string);
if (m.matches()) {
    String url = m.group(1); 
    // Matcher.group(n) returns the nth parenthesized part of the regex
}
```

注意 URL 和 HTML 标签示例中的反斜杠。在 URL 示例中，我们想匹配一个字面上的句号`.`，所以我们必须先将其转义为`\.`以防止它被解释为正则表达式的匹配任意字符操作符，然后我们必须进一步将其转义为`\\.`以防止反斜杠被解释为 Java 字符串转义字符。在 HTML 示例中，我们必须将引号`"`转义为`\"`以防止它结束字符串。反斜杠转义的频率使得正则表达式仍然不够可读。

#### 阅读练习

在 Java 中使用正则表达式

写一个尽可能短的正则表达式来从字符串中移除只包含单词、小写字母的 HTML 标签：

```
String input = "The <b>Good</b>, the <i>Bad</i>, and the <strong>Ugly</strong>";
String regex = "TODO";
String output = input.replaceAll(regex, "");
```

如果期望的输出是`"The Good, the Bad, and the Ugly"`，你可以用什么最短的正则表达式替换 TODO？你可能会发现在[在线 Python 教程中运行此示例](http://www.pythontutor.com/java.html#code=public+class+Regex+%7B%0A++++public+static+void+main(String%5B%5D+args%29+%7B%0A++++++++String+input+%3D+%22The+%3Cb%3EGood%3C/b%3E,+the+%3Ci%3EBad%3C/i%3E,+and+the+%3Cstrong%3EUgly%3C/strong%3E%22%3B+%0A++++++++String+regex+%3D+%22TODO%22%3B%0A++++++++String+output+%3D+input.replaceAll(regex,+%22%22%29%3B%0A++++++++System.out.println(output%29%3B%0A++++%7D%0A%7D&mode=edit&origin=opt-frontend.js&cumulative=false&heapPrimitives=false&textReferences=false&py=java&rawInputLstJSON=%5B%5D)中有所帮助。

缺失答案

缺失解释

### 上下文无关文法

一般来说，一个可以用我们的文法系统表达的语言被称为上下文无关。并不是所有的上下文无关语言都是正则的；也就是说，有些文法无法简化为单个的非递归产生式。我们的 HTML 文法是上下文无关的，但不是正则的。

大多数编程语言的语法也是上下文无关的。通常，任何具有嵌套结构的语言（如嵌套括号或大括号）都是上下文无关的，但不是正则的。该描述适用于 Java 语法，此处部分显示：

```
statement ::= 
  '{' statement* '}'
| 'if' '(' expression ')' statement ('else' statement)?
| 'for' '(' forinit? ';' expression? ';' forupdate? ')' statement
| 'while' '(' expression ')' statement
| 'do' statement 'while' '(' expression ')' ';'
| 'try' '{' statement* '}' ( catches | catches? 'finally' '{' statement* '}' )
| 'switch' '(' expression ')' '{' switchgroups '}'
| 'synchronized' '(' expression ')' '{' statement* '}'
| 'return' expression? ';'
| 'throw' expression ';' 
| 'break' identifier? ';'
| 'continue' identifier? ';'
| expression ';' 
| identifier ':' statement
| ';'
```

## 总结

在计算机科学中，机器处理的文本语言无处不在。语法是描述这种语言的最流行形式，而正则表达式是语法的一个重要子类，可以在没有递归的情况下表示。

今天阅读的主题与我们对好软件的三个特性有关，如下：

+   **免受错误的影响**。语法和正则表达式是字符串和流的声明性规范，可以直接由库和工具使用。这些规范通常更简单、更直接，而且不太可能出错，与手工编写的解析代码相比。

+   **易于理解**。语法捕捉了序列的形状，以一种比手写解析代码更易于理解的形式。不幸的是，正则表达式通常不容易理解，因为它们是可能更易于理解的正则语法的一行简化形式。

+   **易于更改**。语法可以很容易地编辑，但是正则表达式，不幸的是，要改变就难得多，因为复杂的正则表达式晦涩难懂。
