# 第四次课：数据类型复习

## 数据类型构造函数，绑定和使用出现

按照惯例，数据构造函数（在下面的示例中为`Penny`、`Nickel`等）以大写字母开头。有几个例外，比如`true`和`false`。

```
type coin = Penny | Nickel | Dime | Quarter

```

按照惯例，变量（在示例中为`value`和`c`）以小写字母开头。

```
let value (c : coin) : float =
  match c with
    Penny -> 0.01
  | Nickel -> 0.05
  | Dime -> 0.10
  | Quarter -> 0.25

```

上面的例子是一个典型的`match`表达式。但考虑以下变体。看起来应该是相同的，但当我们编译此函数时，OCaml 会抱怨有多余的模式。

```
let bad_value (c : coin) : float =
  let penny = Penny in
    match c with
      penny -> 0.01
    | Nickel -> 0.05
    | Dime -> 0.10
    | Quarter -> 0.25

```

为什么？毕竟，这不就相当于以下内容吗？

```
let bad_value2 (c : coin) : float =
  let penny = Penny in
    if c = penny then 0.01
    else if c = Nickel then 0.05
    else if c = Dime then 0.10
    else if c = Quarter then 0.25
    else raise (Failure "impossible!")

```

不！实际上，更像是

```
let bad_value2 (c : coin) : float =
  let penny = Penny in
    match c with
      random_variable_name -> 0.01
    | Nickel -> 0.05
    | Dime -> 0.10
    | Quarter -> 0.25

```

或者甚至

```
let bad_value3 (c : coin) : float =
  let penny = Penny in
    match c with
      _ -> 0.01
    | Nickel -> 0.05
    | Dime -> 0.10
    | Quarter -> 0.25

```

在匹配表达式中，如果在一个模式的`->`的左侧有一个数据构造函数*C*，那么我们正在比较要匹配的表达式*e*的值是否为*C*。但如果它是一个变量名，那么我们正在声明一个新的、新鲜的变量实例，并将其绑定到*e*的值。因此，下面的任何模式都是多余的，因为这个匹配永远不会失败。

表达式中标识符的出现可以是*绑定*出现或*使用*出现。例如，以下是标识符`id`的所有绑定出现的示例：

```
let id = e (* a value declaration *)
let id = e in ... (* a value declaration *)
let id (arg : s) : t = e (* a function declaration *)
let id (arg1 : s1) (arg2 : s2) : t = e (* a function declaration *)
match e with
  id -> ... (* a pattern match *)

```

相反，几乎任何其他东西，例如

```
if id = e then ... else ...
id + 3

```

是对`id`的使用发生。在这些出现中，`id`被评估，并且其值是在最近的`id`绑定出现中绑定给它的值，无论是作为函数参数、函数声明、值声明还是模式匹配。

在上面的例子中

```
let bad_value (c : coin) : float =
  let penny = Penny in
    match c with
      penny -> 0.01
    | Nickel -> 0.05
    | Dime -> 0.10
    | Quarter -> 0.25

```

在匹配的第一个模式中，OCaml 不会查看`penny`之前是否绑定到`Penny`。在匹配表达式中，`penny`的出现也是一个绑定出现。匹配将成功，并且将`penny`重新绑定到`c`的值。

为什么这样做？最重要的原因是在模式中绑定标识符是一种非常有用的设备，可以使代码简洁、优雅。可以同时在一个模式中绑定几个标识符。例如，

```
match e with
  Add (x, y) :: t -> x + y
| ...

```

同时绑定三个标识符`x`、`y`和`t`，然后可以在右侧的表达式中使用它们。如果不需要值，请在模式中使用通配符`_`，它匹配任何内容。例如，

```
match e with
  Add (x, y) :: _ -> x + y
| ...

```

如果我们想要在模式中允许标识符的使用出现，我们将需要一种方法来区分它们与绑定出现。目前在 OCaml 中没有办法做到这一点。

这里有一个谜题来说明绑定和使用出现之间的区别。以下表达式的值是多少？

```
(*1*)   let f ((x : int), (y : int)) : int =
(*2*)     let x = y in
(*3*)     let y = x in
(*4*)     let (y, x) = (x, y * y) in
(*5*)     match (y, x) with
(*6*)       (x, 1) -> 0
(*7*)     | (x, y) -> x
(*8*)   in f (2, 3)

```

搞清楚这并不容易，但是下面是如何思考的。让我们按照它们在不同绑定出现的行中绑定的值来引用变量。因此，*x*[1]表示`x`在第 1 行绑定的值。

+   第 1 行绑定了三个标识符`f`，`x`和`y`。标识符`x`和`y`绑定到第 8 行提供的`f`的参数。所以 *x*[1] = 2，*y*[1] = 3。标识符`f`绑定到其主体在第 2-7 行给出的函数。

+   第 2 行包含了`x`的绑定出现和`y`的使用出现。所以 *x*[2] = *y*[1] = 3。

+   第 3 行包含了`y`的绑定出现和`x`的使用出现。`x`最接近的绑定在第 2 行。所以 *y*[3] = *x*[2] = 3。

+   第 4 行包含了`x`和`y`的绑定出现在`=`的左边，并且在`=`的右边使用了这两个标识符。使用出现从最近的前一个绑定中获取它们的值。所以 *y*[4] = *x*[2] = 3，而 *x*[4] = *y*[3] * *y*[3] = 9。

+   第 5 行只包含`x`和`y`的使用出现。值分别为 *x*[4] = 9 和 *y*[4] = 3。它初始化了元组（*y*[4]，*x*[4]）= (3, 9) 的模式匹配。

+   第 6 行将 *x*[6] 绑定到 *y*[4] = 3，并尝试将 (3, 9) 与 (3, 1) 进行匹配。匹配失败，所以我们继续下一个模式。

+   第 7 行将 *x*[7] 绑定到 *y*[4] = 3，并将 *y*[7] 绑定到 *x*[4] = 9，并尝试将 (3, 9) 与 (3, 9) 进行匹配。匹配成功，`->` 右侧的值是 *x*[7] = 3，这也是整个表达式的值。

* * *

## 使用多态性

### 列表类型

由于列表非常有用，OCaml 提供了一个内置的参数化列表类型称为`list`。它的行为就像我们在讲座中定义的`List`类型一样，只是构造函数的名称已更改。构造函数`[]`生成一个空列表（与`Nil`相比较），构造函数`::`通过在另一个列表之前添加第一个元素来构建一个新列表（与`Cons`相比较）。就好像`list`被声明为：

```
type 'a list = [] | :: of 'a * 'a list

```

（尽管由于 OCaml 的命名惯例，我们实际上不能这样做）。构造函数`::`是一个中缀操作符，这在表示上非常方便。OCaml 解释器也知道如何很好地打印列表。空列表打印为`[]`，非空列表使用分号分隔的项目在括号内打印。这些形式也可用于编写列表。注意，`[]`是类型为`'a list`的多态值；它对于所有类型`T list`都充当空列表。以下是一些展示列表工作原理的示例：

```
[];;
*- : 'a list = []*
let it = 2 :: [];;
*val it : int list = [2]*
let both = 1 :: it;;
*val both : int list = [1; 2]*
let both2 =
match both with
  x :: lst -> lst 
| [] -> [];;
*val both2 : int list = [2]*
let both3 =
match both2 with
  x :: lst -> lst 
| [] -> [];;
(* we don't "recover polymorphism" here; it would be unsafe in general *)
*val both3 : int list = []*
both = 1 :: 2 :: [];; 
(* we can test lists for equality if we can test their elements *)
*- : bool = true*
match both with
  [x; y] -> x + y (* we can use bracket notation in patterns *)
| _ -> 0;;
*- : int = 3*
[[]];;
*- : 'a list list = [[]]*

```

就像类型一样，我们必须确保在`match`表达式中编写全面的模式：

```
match ["hello"; "goodbye"] with
  s :: _ -> s ^ " hello";;
*Warning P: this pattern-matching is not exhaustive. Here is an example of a value that is not matched: []*

```

内置列表带有[许多有用的预定义 OCaml 库函数](http://caml.inria.fr/pub/docs/manual-ocaml/libref/List.html)，例如以下等等：

```
val List.length : 'a list -> int
val @ : ('a list * 'a list) -> 'a list		(* append two lists *)
val List.hd : 'a list -> 'a
val List.tl : 'a list -> 'a list
val List.nth : ('a list * int) -> 'a

```

当然，所有这些函数也可以轻松地为我们自己定义的`List`类型实现。

### 多个类型参数

我们在课堂上看到了 OCaml 的两个相关特性：

+   能够生成类型提及类型变量的多态值，以及

+   能够相对于任意类型变量对类型进行参数化。

多态值通常是函数值，但也存在其他多态值，比如 `[]`（以及我们定义的 `Nil`）。实际上，数据类型可以根据多个类型参数进行参数化。例如，以下类型 `ortype` 是一个类型级函数，接受一对类型并产生一个新类型：

```
# type ('a, 'b) ortype = Left of 'a | Right of 'b | Both of 'a * 'b;;
*type ('a, 'b) ortype = Left of 'a | Right of 'b | Both of 'a * 'b*
# Left 2;;
*- : (int, 'a) ortype = Left 2*
# Right "hello";;
*- : ('a, string) ortype = Right "hello"*
# Both (true, 'a');;
*- : (bool, char) ortype = Both (true, 'a')*

```

注意值 `Left 2` 和 `Right "hello"` 在一种类型上仍然是多态的。OCaml 总是从 `'a` 开始计算类型变量，因此上面 `Left 2` 匹配中的 `*- : (int, 'a) ortype*` 而不是 `*- : (int, 'b) ortype*`。

### 参数化类型选项

另一个重要的标准参数化类型是`option`，它表示值的*可能*存在性。其定义如下：

```
type 'a option = Some of 'a | None

```

选项在某种类型的有用值没有意义时通常被使用。这对应于 Java 中 `null` 的一些用法（`None`的行为类似于`null`）。不同之处在于，使用 `option` 是类型安全的。只要你使用模式匹配与 `option`，就不会出现运行时空指针异常的危险，因为类型系统强制你考虑到`None`的可能性。应该使用模式匹配来检查并提取值。[更详细的选项描述](http://ocaml-lib.sourceforge.net/doc/Option.html)在 OCaml 库文档中可用。
