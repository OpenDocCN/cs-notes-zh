# 第十讲：函子

在上一堂课中，我们通过 OCaml 中的`module`机制主要考虑了抽象和模块化设计。今天我们将考虑另一种抽象方法，称为*函子*，这是一种构造，使模块能够通过将模块参数化为其他模块来进行组合。

考虑我们在过去几堂课中所看到的`SET`数据抽象：

```
module type SET = sig
  type 'a set
  val empty : 'a set
  val mem : 'a -> 'a set -> bool
  val add : 'a -> 'a set -> 'a set
  val rem : 'a -> 'a set -> 'a set
  val size: 'a set -> int
  val union: 'a set -> 'a set -> 'a set
  val inter: 'a set -> 'a set -> 'a set
end

```

虽然这个接口使用多态来使得能够创建具有不同类型元素的集合，但是这个签名的任何实现都需要使用内置的`=`函数来测试元素是否属于这样的集合。因此，我们例如不能拥有一个字符串集合，其中元素的比较以不区分大小写的方式进行，或者一个整数集合，其中元素在它们的大小（绝对值）相等时被视为相等。我们可以编写两个单独的签名，一个用于具有字符串元素的集合，一个用于具有整数元素的集合，然后在每个签名的实现中使用适当的比较函数。但是这将产生大量几乎重复的代码，无论是在签名中还是在实现中。这样几乎重复的代码编写和维护起来更费力，更重要的是，当一个地方改变而另一个地方没有改变时，往往是 bug 的源头。

*函子*是从模块到模块的映射。它允许构建一个由一个或多个其他模块参数化的模块。函子使我们能够创建一个由另一个进行相等性测试的模块参数化的集合模块，从而允许相同的代码用于不同的相等性测试。为了具体化这一点，我们将考虑以下签名的示例：

```
module type EQUAL = sig
  type t
  val equal : t -> t -> bool
end

```

```
module type SETFUNCTOR =
  functor (Equal : EQUAL) ->
    sig
      type elt = Equal.t
      type set
      val empty : set
      val mem : elt -> set -> bool
      val add: elt -> set -> set
      val size: set -> int
    end

```

签名`EQUAL`描述了函子的输入类型。要实现`EQUAL`，模块只需指定一个类型`t`和一个比较函数`equal: t -> t -> bool`，但这可以是任何东西。

签名`SETFUNCTOR`描述了函子的类型。这与`SET`接口在几个方面有所不同。首先，关键字`functor`表示它是一个接受参数的函子，在这种情况下，参数是任何类型为`EQUAL`的模块。注意语法如何让人想起函数的符号。参数在`SETFUNCTOR`的主体中通过名称`Equal`引用，但这不必是它的实际名称。

`SETFUNCTOR`的主体描述了将要生成的模块的类型。在主体中，与`SET`的多态`'a`不同，元素的类型被命名为`elt`，并被定义为与模块`t`相同的类型`Equal`，无论那是什么。还有一个固定但未指定的类型`set`，以及一些具有适当类型的集合操作，以`elt`和`set`为基础指定。（为了简化表达，我们省略了一些操作，尽管可以轻松地将它们添加回去。）

现在我们准备定义一个实现 `SETFUNCTOR` 签名的函数子。

```
module MakeSet : SETFUNCTOR =
  functor (Equal : EQUAL) ->
    struct
      open Equal
      type elt = t
      type set = elt list
      let empty = []
      let mem x = List.exists (equal x)
      let add x s = if mem x s then s else x :: s
      let size = List.length
    end

```

首先，头文件

```
module MakeSet : SETFUNCTOR =

```

表示我们正在定义一个名为 `MakeSet` 的实现，类型为 `SETFUNCTOR` 的函数子。第二行

```
  functor (Equal : EQUAL) ->

```

表明我们正在定义一个参数为 `EQUAL` 类型的函数子，模块实现了 `EQUAL` 的名称在 `MakeSet` 主体中被引用为 `Equal`，但这不一定是它的实际名称。一般来说，可以有任意数量的参数模块，每个模块必须用名称和签名来指定。请注意，这些参数只能是模块，包括其他参数化模块，它们不能是语言的一级对象，例如函数或其他类型。

最后，`MakeSet` 在 `struct` 和 `end` 之间的主体描述了输出模块的实现。此模块必须满足 `SETFUNCTOR` 主体中描述的签名。

`MakeSet` 的主体就像任何其他模块的主体一样。在这个例子中，使用了 `open` 指令，以便可以在不将它们标记为 `Equal.t` 和 `Equal.equal` 的情况下使用名称 `t` 和 `equal`。

值得注意的是在以下代码中 `equal` 和 `List.exists` 的部分评估：

```
  let mem x = List.exists (equal x)

```

要完整地写出来，我们可能会写成

```
  let mem x s = List.exists (fun y -> equal x y) s

```

但较短的版本是等效的。在两种情况下，我们都使用了 `fun z -> f z` 等价于 `f` 的事实。例如，`fun y -> equal x y` 和 `equal x` 都是检查给定元素是否等于 `x` 值的函数。

现在我们展示如何使用函数子 `MakeSet` 创建模块。为此，我们需要一个实现 `EQUAL` 签名的模块。例如，假设我们想要以不区分大小写的方式测试字符串的相等性。下面是一个执行此操作的模块。

```
module StringNoCase =
  struct
    type t = string
    let equal s1 s2 =
      String.lowercase s1 = String.lowercase s2
  end

```

现在我们可以使用 `MakeSet` 将其应用于 `StringNoCase` 来创建一个具有不区分大小写相等性的字符串集合模块：

```
module SSet = MakeSet (StringNoCase)

```

评估这个表达式，解释器会打印出：

```
module SSet :
  sig
    type elt = StringNoCase.t
    type set = MakeSet(StringNoCase).set
    val empty : set
    val mem : elt -> set -> bool
    val add : elt -> set -> set
    val size : set -> int
  end

```

也就是说，`SSet` 模块定义了类型 `set` 和 `elt`，以及函数 `mem`、`add` 和 `size`，但实际的实现是隐藏的。

现在我们可以使用这个集合抽象来创建和操作具有不区分大小写比较的字符串集合。

```
# let s = SSet.add "I like CS 3110" SSet.empty;;
val s : SSet.set = <abstr>
# SSet.mem "i LiKe cs 3110" s;;
- : bool = true
# SSet.size s;;
- : int = 1

```

在这样做之后，使用绝对值比较创建整数集合模块几乎不需要额外的代码。我们只需要创建另一个实现 `EQUAL` 并将其用作 `MakeSet` 的参数的模块：

```
module IntAbs = struct
  type t = int
  let equal i1 i2 = abs i1 = abs i2
end

module ISet = MakeSet (IntAbs)

```

现在我们可以使用这个集合抽象来创建和操作具有绝对值比较的整数集合：

```
# let i = ISet.add 1 ISet.empty;;
val i : ISet.set = <abstr>
# ISet.mem 1 i;;
- : bool = true
# ISet.mem (-1) i;;
- : bool = true
# ISet.size i;;
- : int = 1
# let i = ISet.add (-1) i;;
val i : ISet.set = <abstr>
# ISet.size i;;
- : int = 1

```

## 注意事项

函数子有一些值得注意的细微之处。首先，请注意在我们定义 `StringNoCase` 时我们没有指定签名 `EQUAL`。我们可以写成

```
module StringNoCase : EQUAL =
  struct
    type t = string
    let equal s1 s2 =
      String.lowercase s1 = String.lowercase s2
  end

```

但这是一个不好的主意：

```
# module SSet = MakeSet (StringNoCase);;
module SSet :
  sig
    type elt = StringNoCase.t
    type set = MakeSet(StringNoCase).set
    val empty : set
    val mem : elt -> set -> bool
    val add : elt -> set -> set
    val size : set -> int
  end
# let s = SSet.add "I like CS 3110" SSet.empty;;
Characters 17-33:
  let s = SSet.add "I like CS 3110" SSet.empty;;
                   ^^^^^^^^^^^^^^^^
Error: This expression has type string but is here used with type
         SSet.elt = StringNoCase.t

```

这里的问题是签名`EQUAL`没有在实现`StringNoCase`中暴露类型定义`type t = string`，因此 functor 不能自由地使用该信息。它只能通过其签名处理模块`StringNoCase`。这与通过使用签名隐藏信息的原则一致。因此，生成的模块不知道`StringNoCase.t`实际上是`string`。但是如果我们省略了`EQUAL`，则`StringNoCase`的签名将从实现中推断出：

```
# module StringNoCase =
    struct
      type t = string
      let equal s1 s2 =
        String.lowercase s1 = String.lowercase s2
    end;;
module StringNoCase :
  sig
    type t = string
    val equal : string -> string -> bool
  end

```

（为了清晰起见手动插入格式）。你可以看到这里暴露了类型定义`type t = string`，而 functor 现在可以使用该信息。

## 逆变

另一个不指定签名的好理由是模块可以实现许多不同的签名，并且可以以不同的方式使用。例如，如果我们定义了

```
module StringNoCase =
  struct
    type t = string
    let compare s1 s2 =
      String.compare (String.lowercase s1) (String.lowercase s2)
    let equal s1 s2 = compare s1 s2 = 0
  end

```

那么`StringNoCase`不仅实现了`EQUAL`，还实现了 OCaml 库中的`Map.OrderedType`和`Set.OrderedType`，因此它也可以作为`Map.Make`和`Set.Make`的参数使用。如果我们指定了`EQUAL`，那么我们将排除这些用途。

如果我们愿意，我们总是可以加以限制：

```
# module StringNoCaseEq = (StringNoCase : EQUAL);;
module StringNoCaseEq : EQUAL

```

模块`StringNoCaseEq`和`StringNoCase`具有相同的实现，但它们的签名不同。这非常类似于 Java 中的类型上转型。它是一个*上转型*，因为它从更具体的规范（更少的实例）转向更一般的规范（更多的实例），这意味着它可以作为较少 functor 的参数。这种反向关系称为*逆变*。

## …带有类型…

为了清晰起见，我们可能希望独立于 functor 本身的签名定义 functor 的结果签名。因此，与上述`SETFUNCTOR`的定义相反，我们可能希望编写类似于以下内容的内容

```
module type SET =
  sig
    type elt
    type set
    val empty : set
    val mem : elt -> set -> bool
    val add: elt -> set -> set
    val size: set -> int
  end

module type SETFUNCTOR = functor (Equal : EQUAL) -> SET

```

这里的困难在于我们需要一种方法将`SET`的类型`elt`与`Equal.t`相等。我们可以在先前的`SETFUNCTOR`定义中通过在主体中编写`type elt = Equal.t`来实现，但是在这里当我们定义`SET`时，周围没有参数模块`Equal`。

为了处理这个问题，OCaml 允许您写

```
module type SETFUNCTOR =
  functor (Equal : EQUAL) -> SET with type elt = Equal.t

```

现在它可以链接起这两种类型：

```
# module type SETFUNCTOR =
    functor (Equal : EQUAL) -> SET with type elt = Equal.t;;
module type SETFUNCTOR =
  functor (Equal : EQUAL) ->
    sig
      type elt = Equal.t
      type set
      val empty : set
      val mem : elt -> set -> bool
      val add : elt -> set -> set
      val size : set -> int
    end

```
