# 讲座 12：命令式特性

到目前为止，我们一直在处理 OCaml 的纯函数式片段。也就是说，我们一直在处理不包括计算效果（也称为副作用）的语言子集，除了打印之外。特别是，每当我们编写函数时，我们都没有改变变量或数据。相反，我们总是计算新的数据。例如，当我们为栈、队列或字典等抽象数据类型编写代码时，将项插入数据结构的操作不会影响旧副本的数据结构。相反，我们总是建立一个适当插入项的新数据结构。（请注意，新数据结构可能会引用旧数据结构，因此这并不像听起来那么低效。）

在大多数情况下，以函数式风格编码（即，无副作用）是一件“好事”，因为更容易局部推断代码的行为。例如，当我们编写纯函数式队列或栈时，不必担心队列或栈的非局部变化。然而，在某些情况下，破坏性地修改数据结构比构建新版本更有效或更清晰。在这些情况下，我们需要某种形式的可变数据结构。

像大多数命令式编程语言一样，OCaml 提供了对可变数据结构的支持，但与 C、C++或 Java 等语言不同，它们不是默认的。因此，鼓励程序员默认纯函数式编码，并且只在绝对必要时才使用可变数据结构。此外，与命令式语言不同，OCaml 不提供对可变*变量*的支持。换句话说，在 OCaml 中变量的值不能改变。相反，所有突变都必须通过数据结构进行。

## 引用

OCaml 中只有两种内置的可变数据结构：引用和数组。OCaml 通过原始参数化的`ref`类型支持命令式编程。类型为“int ref”的值是指向内存中位置的指针，该位置包含一个整数。这类似于 C/C++中的“int*”或 Java 中的“Integer”（但不是 Java 中的“int”）。与列表一样，引用是多态的，因此实际上，我们可以有一个引用（即指针）指向任何类型的值。

引用的部分签名如下：

```
  module type REF =
    sig
      type 'a ref

      (* ref(x) creates a new ref containing x *)
      val ref : 'a -> 'a ref

     (* !x is the contents of the ref cell x *)
      val (!) : 'a ref -> 'a

      (* Effects: x := y updates the contents of x
       * so it contains y. *)
      val (:=) : 'a ref -> 'a -> unit
    end
```

引用（ref）就像是一个可以存储单个值的盒子。通过使用`:=`运算符，可以改变盒子中的值作为副作用。重要的是要区分存储在盒子中的值和盒子本身。引用是最简单的**可变**数据结构。可变数据结构是可以被命令式地改变或**突变**的数据结构。

以下代码展示了一个使用引用的示例：

```
    let x : int ref = ref 3 in
    let y : int = !x in
        (x := !x + 1);
        y + !x
    end
```

上面的代码评估为 7。让我们看看为什么：第一行 "let x:int ref = ref 3" 创建一个新的引用单元，将内容初始化为 3，然后返回一个引用（即指针）到该单元，并将其绑定到 x。第二行 "let y:int = !x" 读取由 x 引用的单元的内容，返回 3，然后将其绑定到 y。第三行 "x := (!x) + 1;" 评估 "!x" 以获取 3，将其加一得到 4，然后将 x 引用的单元的内容设置为此值。第四行 "y + (!x)" 返回 y（即 3）和 x 引用的单元的内容（4）的总和。因此，整个表达式评估为 7。

下面是使用引用构建的可变栈的示例：

```
    module type MUTABLE_STACK =
      sig
        (* An 'a mstack is a mutable stack of 'a elements *)
        type 'a mstack

        (* empty () is a new empty stack *)
        val empty : unit -> 'a mstack

        (* Effects: push m x pushes x onto m *)
        val push : 'a mstack -> 'a -> unit

        (* pop m is the head of m.
         * Effects: pops the head off the stack. *)
         val pop : 'a mstack -> 'a option
    end

    module Mutable_Stack : MUTABLE_STACK =
      struct
        (* A mutable stack is a reference
         * to the list of values, with the top
         * of the stack at the head. *)
        type 'a mstack = ('a list) ref

        let empty () : 'a mstack = ref []

        let push (s: 'a mstack) (x: 'a) : unit =
          s := x::(!s)

        let pop (s: 'a mstack) : 'a option =
          match (!s) with
            [] -> None
          | hd::tl -> (s := tl); Some hd
    end

```

一个对你有好处的练习是使用引用（refs）实现队列、优先队列、字典或者我们在课堂上已经见过的任何其他数据结构的可变版本。

## 数组

OCaml 提供的另一种重要的可变数据结构是数组。数组是引用（refs）的泛化，因为它们是一系列内存位置，其中每个位置包含一个不同的值。我们可以将引用单元视为大小为 1 的数组。类型 `t array` 实际上与 Java 数组类型 `t[]` 非常相似。下面是 OCaml 内置的 Array 结构的部分签名。请注意，要使用操作，您要么必须显式地 "`open Array`"，要么在您想要使用操作 *`foo`* 时写 `Array.*foo*`。

```
  module type ARRAY =
    sig
      (* Overview: an 'a array is a mutable fixed-length sequence of
       * elements of type 'a. *)
      type 'a array

      (* make n x is a new array of length n whose elements are
       * all equal to x. *)
      val make : int -> 'a -> 'a array

      (* of_list lst is a new array containing the values in lst *)
      val of_list : 'a list -> 'a array

      (* normally indicates an out-of-bounds array index *)
      exception Invalid_argument of string 

      (* get a i is the ith element in a. If i is
       * out of bounds, raise Invalid_argument "index out of bounds" *)
      val get : 'a array -> int -> 'a

      (* set a i x
       * Effects: Set the ith element of a to x
       * Raise Invalid_argument if i is not a legal index into a *)
      val set : 'a array -> int -> 'a -> unit

      (* length a is the length of a *)
      val length : 'a array -> int

      ...
    end
```

查看 OCaml 文档以获取有关数组可用操作的更多信息。

注意我们已经开始在规范中使用一种新的子句，即**effects**子句。此子句指定操作除返回值外具有的副作用。当一个例程具有副作用时，在规范中明确使用 "Effects:" 这个词是很有用的，以警告用户有副作用。

## 替换模型和引用

到目前为止我们已经见过的替换模型解释了计算的工作原理，只要不使用 OCaml 的命令式特性。该模型将计算描述为一系列重写步骤，其中程序子表达式被另一个替换，直到不再可能进行进一步的重写。然而，命令式特性引入了**状态**的可能性：执行中的 OCaml 程序伴随着一个当前内存状态，随着计算的进行而改变。

我们暂时不想深入讨论内存堆的工作原理，因此我们将使用一个简单的状态抽象模型。内存 *M* 是一组具有自己唯一名称的内存单元。我们将这些名称称为**位置**；位置是硬件级别上内存地址的抽象版本。给定一个位置，我们可以在内存中查找存储在该位置的值。随着程序的执行，一些内存位置的内容可能会发生变化。

一种可视化执行的方式是，内存由大量（实际上是无限的）的盒子组成，每个盒子只能包含一个值。在执行过程中的任何时刻，一些盒子被使用，而其他盒子为空。每个盒子都有一个唯一的名称（它的位置），这个位置可以用来找到具有该名称的单个盒子。给定一个内存，我们总是可以找到一个未使用的盒子。

## 引用操作

引用上的三个主要操作是：使用 `ref` 运算符创建引用，使用 `!` 进行解引用，使用 `:=` 进行更新。每个操作在评估时都有一个相关的规约。为了解释这些操作的作用，需要一种新的表达式类型，表示一个位置。我们将写出语法元变量 *loc* 来表示一个位置。为了解释如何评估 OCaml，我们假设在评估程序时有一个无限大的位置集合（称为 **Loc**）可供使用，即使实际内存是有限的。我们不关心 **Loc** 的元素实际上是什么。我们可以将它们视为内存地址、整数，甚至字符串。重要的是我们能够区分 **Loc** 中的两个不同元素。

### ref

ref 操作创建一个新的位置。一旦其参数是一个值，它就会被规约，创建一个新的位置。

> `ref` *v*`-->` *loc*

新位置 *loc* 是当前内存中未使用的位置。这个评估步骤也具有副作用：名为 *loc* 的内存单元被设置为包含值 *v*。

这个规则将一个 *loc* 表达式引入到运行中的程序中。这与我们到目前为止看到的所有评估规则有点不同，因为 *loc* 表达式不能出现在原始的 OCaml 程序中。这并不是问题；我们必须记住，我们对评估的模型是有用的虚构。只要模型对程序运行时发生的事情给出正确答案，我们就满意。在 OCaml 中，如果一个程序评估为一个位置，它会被打印为一个 ref 表达式。

在 OCaml 中，引用的相等性有点奇怪。如果我们使用 = 测试两个类型为 'a ref 的表达式，OCaml 实际上会检查它们的内容是否相等。这通常是你想要的，但你也可以使用 == 来检查引用本身是否相等（即，这两个引用是否指向相同的内存块）。

```
# let a = ref 2;;
val a : int ref = {contents = 2}
# let b = ref 2;;
val b : int ref = {contents = 2}
# let c = a;;
# a = b;;
- : bool = true
# a == b;;
- : bool = false
# a = c;;
- : bool = true
# a == c;;
- : bool = true

```

### !

解引用（`!`）操作找到存储在给定位置的值：

> `!` *loc*`-->` *v*

当然，替换 `!`*loc* 子表达式的值 v 是在名为 *loc* 的内存单元中找到的值。

### :=

更新（`:=`）操作更新存储在给定位置的值：

> *loc* `:=` *v* `-->` `()`

它求值为单位值，但具有更新名为 *loc* 的内存位置以包含 *v* 的副作用。

## 示例

考虑以下 OCaml 示例：

```
let x = ref 0 in
let y = x in
    x := 1;
    !y

```

```
 What does this evaluate to? We can use the model about to figure it out:
```

```
let x = ref 0 in
let y = x in
    x := 1; !y
Memory: (empty)
-->
let x = *loc1* in
let y = x in
    x := 1; !y
Memory: (loc1 = 0)
-->   (substitute *loc1* for x)
let y = *loc1* in
    *loc1* := 1; !y
Memory: (loc1 = 0)
-->   (substitute *loc1* for y)
*loc1* := 1; !*loc1* Memory: (loc1 = 0)--> !*loc1* Memory: (loc1 = 1)
--> 1
Memory: (loc1 = 1)
```

所以当我们对`y`进行解引用时，可以看到对`x`的更新。这是因为`x`和`y`是**别名**：对同一位置(`*loc1*`)的两个不同名称。
