# 第 8 讲：功能栈和队列、字典、分数

## 功能数据结构

在这个讲座中，我们将看一些实现数据结构的结构和签名的例子。我们将展示堆栈和队列如何以函数式风格高效实现。

什么是功能栈，或者功能队列？它是一种数据结构，其操作不会改变数据结构，而是创建一个新的数据结构，具有适当的修改，而不是在原地更改它。在命令式语言中，数据操作通常支持*破坏性更新* —— “破坏性”是指更新完成后，原始数据结构消失了。函数抽象支持*非破坏性更新*：原始值仍然存在，未经修改，因此使用它的代码不受影响。为了效率，重要的是通过尽可能与原始数据结构共享来实现非破坏性更新，而不是创建全新的数据结构。

## 栈

回想一下栈：后进先出（LIFO）队列。就像列表一样，栈操作基本上不关心存储的值的类型，因此它是一种自然的多态数据结构。

这是功能栈的可能签名：  

```
module type STACK = 
  sig
    (* A stack of elements of type 'a. We write 

    to * denote a stack whose top element is a1, with successive * elements a2, a3,...an. *) type 'a stack exception EmptyStack (* The empty stack. *) val empty : 'a stack (* Whether this stack is empty. *) val isEmpty : 'a stack -> bool (* Returns a new stack with x pushed onto the top. *) val push : ('a * 'a stack) -> 'a stack (* Returns a new stack with the top element popped off. *) val pop : 'a stack -> 'a stack (* The top element of the stack. *) val top : 'a stack -> 'a (* map(f) maps one stack into a corresponding stack, using f. *) val map : ('a -> 'b) -> 'a stack -> 'b stack (* app(f) applies f to every element of the stack, top to bottom. *) val app : ('a -> unit) -> 'a stack -> unit end

```

此签名指定了堆栈的参数化抽象类型。注意类型变量`'a`。该签名还指定了空堆栈值，以及用于检查堆栈是否为空、执行推入、弹出和顶部操作的函数。此外，我们还指定了用于遍历堆栈值的 map 和 app 函数。

我们还声明了一个名为 `EmptyStack` 的异常，在堆栈为空时由 top 和 pop 操作引发。

这是符合上述签名的堆栈的最简实现。它是基于列表实现的。

```
module Stack : STACK = 
  struct
    type 'a stack = 'a list
    exception EmptyStack

    let empty : 'a stack = []

    let isEmpty (l : 'a stack) : bool = l = []

    let push ((x : 'a), (l : 'a stack)) : 'a stack = x :: l

    let pop (l : 'a stack) : 'a stack = 
       match l with 
         [] -> raise EmptyStack
       | x :: xs -> xs

    let top (l : 'a stack) : 'a = 
       match l with
         [] -> raise EmptyStack
       | x :: xs -> x

    let map (f : 'a -> 'b) (l : 'a stack) : 'b stack = List.map f l
    let app (f : 'a -> unit) (l : 'a stack) : unit = List.iter f l
  end

```

到目前为止，我们一直在定义异常，唯一的目的是为了引发它们并中断执行中的程序。就像在 Java 中一样，也可以捕获异常，这在 OCaml 中被称为“处理异常”。

举例来说，考虑以下例子。在上述代码中，我们分别将 top 和 pop 实现为函数，这些函数返回列表的第一个元素和列表的其余部分。OCaml 已经定义了可以执行此操作的函数，即 `List.hd` 和 `List.tl`（分别用于头和尾）。函数 `hd` 接受列表作为参数，并返回列表的第一个元素，如果列表为空，则引发异常 `Failure`。`tl` 的情况类似。人们希望能够简单地在 `Stack` 中编写：

```
let top (l : 'a stack) : 'a = List.hd l
let pop (l : 'a stack) : 'a stack = List.tl l

```

但是，如果传递一个空栈，`top` 和 `pop` 应该引发 `EmptyStack` 异常。如上所写，将引发 `Failure` 异常。我们需要做的是拦截（或处理）异常，并引发正确的异常。以下是一种方法：

```
let top (l : 'a stack) : 'a =
  try List.hd l with Failure _ -> raise EmptyStack
let pop (l : 'a stack) : 'a stack =
  try List.tl l with Failure _ -> raise EmptyStack

```

处理异常的语法如下：

```
try e with exn -> e'

```

其中`e`是要评估的表达式，如果`e`引发与`exn`匹配的异常，那么将评估表达式`e'`。`e`和`e'`的类型必须相同。

## 队列

让我们写一个比栈更有趣的例子。毕竟，从上面可以看出，它们只是列表。考虑队列数据结构，一种先进先出的数据结构。同样，我们考虑函数式队列。这里是一个可能的签名：

```
module type QUEUE =
  sig
    type 'a queue
    exception EmptyQueue

    val empty : 'a queue
    val isEmpty : 'a queue -> bool

    val enqueue : ('a * 'a queue) -> 'a queue
    val dequeue : 'a queue -> 'a queue
    val front : 'a queue -> 'a

    val map : ('a -> 'b) -> 'a queue -> 'b queue
    val app : ('a -> unit) -> 'a queue -> unit      
  end

```

队列的最简单的可能实现是通过两个栈来表示一个队列：一个栈 A 用于入队元素，一个栈 B 用于出队元素。当出队时，如果栈 B 为空，则我们反转栈 A 并将其视为新的栈 B。

这里是这样的队列的一个实现。它使用了栈结构`Stack`，在结构内部重新绑定到名称`S`，以避免过长的标识符名称。

```
module Queue : QUEUE = 
    struct

      module S = Stack

      type 'a queue = ('a S.stack * 'a S.stack)
      exception EmptyQueue

      let empty : 'a queue = (S.empty, S.empty)
      let isEmpty ((s1, s2) : 'a queue) = 
        S.isEmpty s1 && S.isEmpty s2 

      let enqueue ((x : 'a), ((s1, s2) : 'a queue)) : 'a queue = 
        (S.push (x, s1), s2)

      let rev (s : 'a S.stack) : 'a S.stack = 
        let rec loop ((prev : 'a S.stack), (curr : 'a S.stack))
        : 'a S.stack =
          if S.isEmpty prev
          then curr
          else loop (S.pop prev, S.push (S.top prev, curr))
      in
        loop (s, S.empty)

      let dequeue ((s1, s2) : 'a queue) : 'a queue = 
        if S.isEmpty s2
        then try (S.empty, S.pop (rev s1)) 
             with S.EmptyStack -> raise EmptyQueue
        else (s1, S.pop s2)

      let front ((s1, s2) : 'a queue) : 'a = 
        if (S.isEmpty s2)
        then try S.top (rev s1)
             with S.EmptyStack -> raise EmptyQueue
        else S.top s2

      let map (f : 'a -> 'b) ((s1, s2) : 'a queue) : 'b queue = 
        (S.map f s1, S.map f s2)

      let app (f : 'a -> unit) ((s1, s2) : 'a queue) : unit = 
        S.app f s2;
        S.app f (rev s1)

    end

```

我们上周学习了折叠。在上面的实现中，可以使用折叠来执行栈反转。但是，由于 Stack 模块没有指定折叠操作，并且将 Stack 的实现隐藏在 Queue 模块中，所以我们需要更多。Stack 签名应该指定一个折叠操作，以帮助其用户遍历其元素。

## 字典

一个非常有用的抽象是*字典*：从字符串到其他值的映射。一个更通用的字典，将一个任意键类型映射到另一个键类型，通常称为*映射*或*关联数组*，尽管有时也使用“字典”这个词。无论如何，实现技术都是相似的。这里是字典的一个接口：

```
module type DICTIONARY =
    sig
        (* An 'a dict is a mapping from strings to 'a.
           We write {k1->v1, k2->v2, ...} for the dictionary which
           maps k1 to v1, k2 to v2, and so forth. *)
        type key = string
    type 'a dict

        (* make an empty dictionary carrying 'a values *)
        val make : unit -> 'a dict

        (* insert a key and value into the dictionary *)
        val insert : 'a dict -> key -> 'a -> 'a dict

        (* Return the value that a key maps to in the dictionary.
         * Raise NotFound if there is not mapping for the key. *)
        val lookup : 'a dict -> key -> 'a
        exception NotFound

        (* applies a function to all the elements of a dictionary;
           i.e., if a dictionary d maps a string s to an element a, 
           then the dictionary (map f d) will map s to f(a). *)
        val map : ('a -> 'b) -> 'a dict -> 'b dict

   end

```

这是一个使用*关联列表* `[(key1, x1); ...; (keyn, xn)]` 的实现

```
module AssocList : DICTIONARY =
    struct
       type key = string
       type 'a dict = (key * 'a) list

       (* AF: The list [(k1,v1), (k2,v2), ...] represents the dictionary
        * {k1 -> v1, k2 -> v2, ...}, except that if a key occurs
        * multiple times in the list, only the earliest one matters.
        * RI: true.
        *)

       let make() : 'a dict = []

       let insert (d : 'a dict) (k : key) (x : 'a) : 'a dict =
           (k, x) :: d

       exception NotFound

       let rec lookup (d : 'a dict) (k : key) : 'a =
         match d with
           [] -> raise NotFound
         | (k', x) :: rest ->
              if k = k' then x
              else lookup rest k

       let map (f : 'a -> 'b) (d : 'a dict) =
           List.map (fun (k, a) -> (k, f a)) d
    end

```

这是另一种使用高阶函数作为字典的实现。

```
module FunctionDict : DICTIONARY =
  struct
    type key = string
    type 'a dict = string -> 'a
    (* The function f represents the mapping in which x is mapped to
     * (f x), except for x that are not in the mapping, in which case
     * f raises NotFound.
     *)
    exception NotFound
    let make () = fun _ -> raise NotFound
    let lookup (d : 'a dict) (key : string) : 'a = d key
    let insert (d : 'a dict) (k : key) (x : 'a) : 'a dict =
      fun k' -> if k = k' then x else d k'
    let map (f : 'a -> 'b) (d : 'a dict) = fun k -> f (d k)
  end    

```

下一个实现似乎更适合查找值。还要注意，抽象函数不需要指定重复键的含义。

```
module SortedAssocList : DICTIONARY =
    struct
        type key = string
        type 'a dict = (key * 'a) list

        (* AF: The list [(k1, v1); (k2, v2); ...] represents
         *     the dictionary {k1 -> v1, k2 -> v2, ...}
         * RI: The list is sorted by key and each key occurs only once
         *     in the list. *)

        let make() : 'a dict = []

        let rec insert (d : 'a dict) (k : key) (x : 'a) : 'a dict =
          match d with
            [] -> (k, x) :: []
          | (k', x') :: rest ->
              match String.compare k k' with
                1 -> (k', x') :: (insert rest k x)
              | 0 -> (k, x) :: rest
              | -1 -> (k, x) :: (k', x') :: rest
              | _ -> failwith "Impossible"

        exception NotFound

        let rec lookup (d : 'a dict) (k : key) : 'a =
          match d with
            [] -> raise NotFound
          | (k', x) :: rest ->
              match String.compare k k' with
                0 -> x
              | -1 -> raise NotFound
              | 1 -> lookup rest k
              | _ -> failwith "Impossible"

        let map (f : 'a -> 'b) (d : 'a dict) =
           List.map (fun (k,a) -> (k, f a)) d
    end

```

这里是字典的另一个实现。这个实现使用二叉树来保持数据。希望插入或查找将与 log n 成比例，其中 n 是树中项目的数量。

```
module AssocTree : DICTIONARY =
    struct
        type key = string
        type 'a dict = Empty | Node of key * 'a * 'a dict * 'a dict

        (* AF: Empty represents the empty mapping {}
         *     Node (key, datum, left, right) represents the union of
         *     the mappings {key -> datum}, AF(left), and AF(right).
         * RI: for Nodes, data to the left have keys that
         *     are LESS than the datum and the keys of
         *     the data to the right. *)

        let make() : 'a dict = Empty

        let rec insert (d : 'a dict) (k : key) (x : 'a) : 'a dict =
          match d with
            Empty -> Node (k, x, Empty, Empty)
          | Node (k', x', l, r) ->
              match String.compare k k' with
                0 -> Node(k, x, l, r)
              | -1 -> Node(k', x', insert l k x, r)
              | 1 -> Node(k', x', l, insert r k x)
              | _ -> failwith "Impossible"

        exception NotFound

        let rec lookup (d : 'a dict) (k : key) : 'a =
          match d with
            Empty -> raise NotFound
          | Node(k', x, l, r) ->
              match String.compare k k' with
                0 -> x
              | -1 -> lookup l k
              | 1 -> lookup r k
              | _ -> failwith "Impossible"

        let rec map (f : 'a -> 'b) (d : 'a dict) =
          match d with
            Empty -> Empty
          | Node (k, x, l, r) -> Node (k, f x, map f l, map f r)
    end

```

## 分数

另一种简单的数据类型是分数，两个整数的比例。这是一个可能的接口：

```
module type FRACTION =
    sig
        (* A fraction is a rational number p/q, where q != 0.*)
        type fraction
        (* Returns: make n d  is n/d. Requires: d != 0\. *)
        val make : int -> int -> fraction
        val numerator : fraction -> int
        val denominator : fraction -> int
        val toString : fraction -> string
        val toReal : fraction -> float
        val add : fraction -> fraction -> fraction
        val mul : fraction -> fraction -> fraction
   end

```

这里是分数的一个实现--这里可能出什么问题？

```
module Fraction1 : FRACTION =
    struct
        type fraction = { num:int; denom:int }
        (* AF: The record {num=n; denom=d} represents fraction (n/d) *)

        let make (n : int) (d : int) = {num=n; denom=d}

        let numerator (x : fraction) : int = x.num

        let denominator (x : fraction) : int = x.denom

        let toString (x : fraction) : string =
            (string_of_int (numerator x)) ^ "/" ^
            (string_of_int (denominator x))

        let toReal (x : fraction) : float =
            (float (numerator x)) /. (float (denominator x))

        let mul (x : fraction) (y : fraction) : fraction =
            make ((numerator x) * (numerator y))
                 ((denominator x) * (denominator y))

        let add (x : fraction) (y : fraction) : fraction =
            make ((numerator x) * (denominator y) +
                  (numerator y) * (denominator x))
                 ((denominator x) * (denominator y))
    end

```

这种实现存在一些弱点。最好检查分母。其次，我们没有化简到最简形式。因此，我们可能比需要更快地溢出。也许我们不想允许负分母。

我们应该选择一个描述我们将如何表示合法分数的表示不变式。这里是一个选择：

```
module Fraction2 : FRACTION =
    struct
        type fraction = { num:int; denom:int }
        (* AF: represents the fraction num/denom
         * RI:
         *  (1) denom is always positive
         *  (2) always in most reduced form
         *)

        (* Returns the greatest common divisor of x and y.
         * Requires: x, y are positive.
         * Implementation: Euclid's algorithm.
         *)
        let rec gcd (x : int) (y : int) : int =
                if x = 0 then y
                else if x < y then gcd (y - x) x
                else gcd y (x - y)

        exception BadDenominator

        let make (n : int) (d : int) : fraction =
            if d = 0 then raise BadDenominator
            else let g = gcd (abs n) (abs d) in
                 let n2 = n / g in
                 let d2 = d / g
            in
                 if (d2 < 0) then {num = -n2; denom = -d2}
                 else {num = n2; denom = d2}

        let numerator (x : fraction) : int = x.num

        let denominator (x : fraction) : int = x.denom

        let toString (x : fraction) : string =
            (string_of_int (numerator x)) ^ "/" ^
            (string_of_int (denominator x))

        let toReal (x : fraction) : float =
            (float (numerator x)) /. (float (denominator x))

        (* Notice that we didn't have to re-code mul or add --
         * they automatically get reduced because we called
         * make instead of building the data structure directly.
         *)
        let mul (x : fraction) (y : fraction) : fraction =
            make ((numerator x) * (numerator y))
                 ((denominator x) * (denominator y))

        let add (x : fraction) (y : fraction) : fraction =
            make ((numerator x) * (denominator y) +
                  (numerator y) * (denominator x))
                 ((denominator x) * (denominator y))
    end

```
