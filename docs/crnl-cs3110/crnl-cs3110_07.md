# 第五讲：多态性和模式匹配

## 变体类型

列表非常有用，但事实证明它们并不像看起来那么特殊。我们可以实现我们自己的列表，以及其他更有趣的数据结构，例如二叉树。

我们已经看到了一些简单的**变体类型**的示例，有时被称为**代数数据类型**或简称为**数据类型**。变体类型提供了一些必要的功能：能够有一个变量包含多种类型的值。

与元组类型和函数类型不同，但与记录类型相同，变体类型不能是匿名的；它们必须用它们的名称声明。假设我们想要一个变量，它可以包含三个值之一：Yes、No 或 Maybe，非常类似于 Java 中的 `enum`。其类型可以声明为一个变体类型：

```
# type answer = Yes | No | Maybe;;
*type answer = Yes | No | Maybe*
# let x : answer = Yes;;
*val x : answer = Yes*

```

`type` 关键字声明了新类型的名称。变体类型是用一组**构造函数**声明的，描述了创建该类型值的可能方法。在这种情况下，我们有三个构造函数：`Yes`、`No` 和 `Maybe`。构造函数名称必须以大写字母开头，而 OCaml 中的所有其他名称必须以小写字母开头。

不同的构造函数也可以携带其他值。例如，假设我们想要一个类型，它可以是 2D 点或 3D 点。它可以声明如下：

```
type eitherPoint = TwoD of float * float
                 | ThreeD of float * float * float

```

类型 `eitherPoint` 的一些值的示例包括：`TwoD (2.1, 3.0)` 和 `ThreeD (1.0, 0.0, -1.0)`。

假设我们有一个类型为 `eitherPoint` 的值，它可以是某个值的 `TwoD` 或 `ThreeD`。我们需要一种方法来提取“某个值”。这可以通过模式匹配来实现。例如：

```
let lastTwoComponents (p : eitherPoint) : float * float =
    match p with
      TwoD (x, y) -> (x, y)
    | ThreeD (x, y, z) -> (y, z)

```

## 变体类型语法

我们使用 *X* 作为元变量来表示构造函数的名称，*T* 来表示类型的名称。方括号 [ ] 表示可选的语法元素。然后，变体类型声明的一般形式如下所示：

> `type`*T* = *X*[1][`of` *t*[1]] | ... | *X[n]* [`of` *t**[n]*]

变体类型引入了术语 *e*、模式 *p* 和值 *v* 的新语法：

> *e* ::= ... |  *X e* |  `match`* e *`with `*p*[1] `->` *e*[1]| ... | *p[n]* `->` *e[n]**p* ::=  *X  *|  *X*`(`*x*[1] : *t*[1], ...,  *x[n]* : *t[n]*`)`
> 
> *v* ::= *c* | (*v*[1], ..., *v[n]*) | `fun` *p* `->` *e* | *X v*

注意表达式 "`match`* e *`with `*p*[1] `->` *e*[1 ]| ... | *p[n]* `->` *e[n]*" 中的竖线是该结构的语法的一部分；其他竖线 (|) 是 BNF 符号的一部分。

我们可以使用变体类型来定义许多有用的数据结构。事实上，`bool` 实际上只是一个具有名称为 `true` 和 `false` 的构造函数的变体类型。

## 实现整数列表

我们可以使用变体类型编写我们自己的列表版本。假设我们想要定义表现为整数链表的值。链表要么为空，要么有一个整数，后面跟着包含其余列表元素的另一个列表。这导致一个非常自然的变体类型声明：

```
type intlist = Nil | Cons of (int * intlist)

```

此类型有两个构造函数，`Nil`和`Cons`。它是一个**递归类型**，因为它在自己的定义中（在`Cons`构造函数中）提到了自己，就像递归函数是在自己的定义中提到了自己一样。

任何整数列表都可以用这种类型来表示。例如，空列表只是构造函数`Nil`，而`Cons`对应于操作符`::`。以下是一些列表的例子：

```
let list1 = Nil                     (* the empty list: [] *)
let list2 = Cons (1, Nil)           (* the list containing just 1: [1] *)
let list3 = Cons (2, Cons (1, Nil)) (* the list [2; 1] *)
let list4 = Cons (2, list2)         (* also the list [2; 1] *)
(* the list [1; 2; 3; 4; 5] *)
let list5 = Cons (1, Cons (2, Cons (3, Cons (4, Cons (5, Nil)))))
(* the list [6; 7; 8; 9; 10] *)
let list6 = Cons (6, Cons (7, Cons (8, Cons (9, Cons (10, Nil)))))

```

因此，我们可以构造任意我们想要的列表。我们也可以使用模式匹配来拆解它们。例如，我们上面的`length`函数可以通过将列表模式转换为相应的使用构造函数的模式来为`intlist`编写。同样，我们可以实现许多其他对列表的函数，如下面的例子所示。

```
(* An intlist is either Nil or Cons of an int and a (shorter) intlist *)
type intlist = Nil | Cons of int * intlist

(* Returns the length of lst *)
let rec length (lst : intlist) : int =
  match lst with
  | Nil -> 0
  | Cons (h, t) -> length t + 1

(* is the list empty? *)
let is_empty (lst : intlist) : bool =
  match lst with
  | Nil -> true
  | Cons _ -> false

(* Notice that the match expressions for lists all have the same
 * form -- a case for the empty list (Nil) and a case for a Cons.
 * Also notice that for most functions, the Cons case involves a
 * recursive function call. *)

(* Return the sum of the elements in the list *)
let rec sum (lst : intlist) : int =
  match lst with
  | Nil -> 0
  | Cons (i, t) -> i + sum t

(* Create a string representation of a list *)
let rec to_string (lst : intlist) : string =
  match lst with
  | Nil -> ""
  | Cons (i, Nil) -> string_of_int i
  | Cons (i, Cons (j, t)) ->
      string_of_int i ^ "," ^ to_string (Cons (j, t))

(* Return the head (first element) of the list *)
let head (lst : intlist) : int =
  match lst with
  | Nil -> failwith "empty list"
  | Cons (i, t) -> i

(* Return the tail (rest of the list after the head) *)
let tail (lst : intlist) : intlist =
  match lst with
  | Nil -> failwith "empty list"
  | Cons (i, t) -> t

(* Return the last element of the list (if any) *)
let rec last (lst : intlist) : int =
  match lst with
  | Nil -> failwith "empty list"
  | Cons (i, Nil) -> i
  | Cons (i, t) -> last t

(* Return the nth element of the list (starting from 0) *)
let rec nth (lst : intlist) (n : int) : int =
  match lst with
  | Nil -> failwith "index out of bounds"
  | Cons (i, t) ->
      if n = 0 then i
      else nth t (n - 1)

(* Append two lists:  append [1; 2; 3] [4; 5; 6] = [1; 2; 3; 4; 5; 6] *)
let rec append (l1 : intlist) (l2 : intlist) : intlist =
  match l1 with
  | Nil -> l2
  | Cons (i, t) -> Cons (i, append t l2)

(* Reverse a list:  reverse [1; 2; 3] = [3; 2; 1].
 * First reverse the tail of the list 
 * (e.g., compute reverse [2; 3] = [3; 2]), then
 * append the singleton list [1] to the end to yield [3; 2; 1].
 * This is not the most efficient method. *)
let rec reverse (lst : intlist) : intlist =
  match lst with
  | Nil -> Nil
  | Cons (h, t) -> append (reverse t) (Cons (h , Nil))

(******************************
 * Examples
 ******************************)

(* Here is a way to perform a function on each element
 * of a list.  We apply the function recursively.
 *)

let inc (x : int) : int = x + 1
let square (x : int) : int = x * x

(* Given [i1; i2; ...; in], return [i1+1; i2+1; ...; in+n] *)
let rec addone_to_all (lst : intlist) : intlist = 
  match lst with
  | Nil -> Nil
  | Cons (h, t) -> Cons (inc h, addone_to_all t)

(* Given [i1; i2; ...; in], return [i1*i1; i2*i2; ...; in*in] *)
let rec square_all (lst : intlist) : intlist = 
  match lst with
  | Nil -> Nil
  | Cons (h, t) -> Cons (square h, square_all t)

(* Here is a more general method. *)

(* Given a function f and [i1; ...; in], return [f i1; ...; f in].
 * Notice how we factored out the common parts of addone_to_all
 * and square_all. *)
let rec do_function_to_all (f : int -> int) (lst : intlist) : intlist =
  match lst with
  | Nil -> Nil
  | Cons (h, t) -> Cons (f h, do_function_to_all f t)

let addone_to_all (lst : intlist) : intlist =
  do_function_to_all inc lst

let square_all (lst : intlist) : intlist =
  do_function_to_all square lst

(* Even better: use anonymous functions. *)

let addone_to_all (lst : intlist) : intlist =
  do_function_to_all (fun x -> x + 1) lst

let square_all (lst : intlist) : intlist =
  do_function_to_all (fun x -> x * x) lst

(* Equivalently, we can partially evaluate by applying 
 * do_function_to_all just to the first argument. *)

let addone_to_all : intlist -> intlist =
  do_function_to_all (fun x -> x + 1)

let square_all : intlist -> intlist =
  do_function_to_all (fun x -> x * x)

(* Say we want to compute the sum and product of integers
 * in a list. *)

(* Explicit versions *)
let rec sum (lst : intlist) : int =
  match lst with
  | Nil -> 0
  | Cons (i, t) -> i + sum t

let rec product (lst : intlist) : int =
  match lst with
  | Nil -> 1
  | Cons (h, t) -> h * product t

(* Better: use a general function collapse that takes an
 * operation and an identity element for that operation.
 *)

(* Given f, b, and [i1; i2; ...; in], return f(i1, f(i2, ..., f (in, b))).
 * Again, we factored out the common parts of sum and product. *)
let rec collapse (f : int -> int -> int) (b : int) (lst : intlist) : int =
  match lst with
  | Nil -> b
  | Cons (h, t) -> f h (collapse f b t)

(* Now we can define sum and product in terms of collapse *)
let sum (lst : intlist) : int =
  let add (i1 : int) (i2 : int) : int = i1 + i2 in
  collapse add 0 lst

let product (lst : intlist) : int =
  let mul (i1 : int) (i2 : int) : int = i1 * i2 in
  collapse mul 1 lst

(* Here, we use anonymous functions instead of defining add and mul.
 * After all, what's the point of giving those functions names if all
 * we're going to do is pass them to collapse? *)
let sum (lst : intlist) : int =
  collapse (fun i1 i2 -> i1 + i2) 0 lst

let product (lst : intlist) : int =
  collapse (fun i1 i2 -> i1 * i2) 1 lst

(* Trees of integers *)

type inttree = Empty | Node of node
and node = { value : int; left : inttree; right : inttree }

(* Return true if the tree contains x. *)
let rec search (t : inttree) (x : int) : bool =
  match t with
  | Empty -> false
  | Node {value=v; left=l; right=r} ->
	v = x || search l x || search r x

let tree1 =
  Node {value=2; left=Node {value=1; left=Empty; right=Empty};
                 right=Node {value=3; left=Empty; right=Empty}}

let z = search tree1 3

```

### 使用递归类型表示树

树是另一种非常有用的数据结构。与列表不同，它们不是内置到 OCaml 中的。二叉树要么是

+   空树（没有子节点），或者

+   包含一个值和两个子树（二叉树）的节点。

为了变化一下，让我们使用记录类型来表示树节点。在 OCaml 中，我们必须定义两种相互递归的类型，一个用于表示树节点，另一个用于表示（可能为空的）树：

```
type inttree = Empty | Node of node
and node = { value : int; left : inttree; right : inttree }

```

相互递归类型声明何时合法的规则有点棘手。基本上，任何递归类型的循环必须包含至少一个记录类型或变体类型。由于`inttree`和`node`之间的循环包括两种类型，因此此声明是合法的。

```
  2
 / \        Node {value=2; left=Node {value=1; left=Empty; right=Empty};
1   3                      right=Node {value=3; left=Empty; right=Empty}}

```

因为树节点中存储了几个东西，使用记录而不是元组来保持它们的清晰是有帮助的。但元组也可以工作。

我们可以使用模式匹配来编写通常用于递归遍历树的算法。例如，这是一个对树的递归搜索：

```
(* Return true if the tree contains x. *)
let rec search ((t: inttree), (x:int)): bool =
  match t with
      Empty -> false
    | Node {value=v; left=l; right=r} ->
	v = x || search (l, x) || search (r, x)

```

当然，如果我们知道树遵循二叉搜索树的不变性，我们可以编写一个更有效的算法。

### 使用递归类型表示自然数

我们甚至可以定义像数字一样的数据结构，证明我们也不一定需要将数字内置到 OCaml 中！自然数要么是值为零，要么是另一个自然数的*后继*。这个定义自然地导致了对行为像自然数的值`nat`的以下定义：

```
type nat = Zero | Next of nat

```

这是你可能在数理逻辑课程中定义自然数的方式。我们定义了一个新类型`nat`，`Zero`和`Next`是此类型值的构造函数。类型`nat`是一个**递归类型**，它允许我们构建具有任意数量嵌套`Next`构造函数的表达式。这样的值表现得像自然数：

```
let zero = Zero
and one = Next Zero
and two = Next (Next Zero)
let three = Next two
let four = Next three

```

当我们询问解释器 *four* 代表什么时，我们得到

```
four;;
*- : nat = Next (Next (Next (Next Zero)))*

```

相应的 Java 定义将是

```
public interface nat { }
public class Zero implements nat {}
public class Next implements nat {
  nat v;
  Next(nat v) { v = this.v; }
}

nat zero = new Zero();
nat one = new Next(new Zero());
nat two = new Next(new Next(new Zero()));
nat three = new Next(two);
nat four = new Next(three);

```

实际上，实现是类似的。

现在我们可以编写操作此类型值的函数了。

```
let isZero (n : nat) : bool =
  match n with
    Zero -> true
  | Next m -> false

```

在这里，我们正在模式匹配具有类型 `nat` 的值。如果值是 `Zero`，我们会计算为 `true`；否则，我们会计算为 `false`。

```
let pred (n : nat) : nat =
  match n with
    Zero -> failwith "Zero has no predecessor"
  | Next m -> m

```

在这里，我们确定一个数字的前驱。如果 `n` 的值匹配 `Zero`，那么我们会引发一个异常，因为自然数中没有零的前驱。如果值匹配 `Next m`，其中 `m` 的值也必须是 `nat` 类型，那么我们返回 `m`。

类似地，我们可以定义一个函数来相加两个数字：

```
let rec add (n1 : nat) (n2 : nat) : nat =
  match n1 with
    Zero -> n2
  | Next m -> add m (Next n2)

```

如果你尝试评估 `add four four`，解释器会回答：

```
add four four;;
*- : nat = Next (Next (Next (Next (Next (Next (Next (Next Zero)))))))* 
```

这是 8 的 `nat` 表示。

为了更好地理解我们计算的结果，我们希望将这些值转换为 `int` 类型：

```
let rec toInt (n : nat) : int =
  match n with
    Zero -> 0
  | Next n -> 1 + toInt n

```

这相当容易。现在我们可以写 `toInt (add four four)` 并得到 *8*。逆操作呢？

```
let rec toNat (i : int) : nat =
  if i < 0 then failwith "toNat on negative number"
  else if i = 0 then Zero
  else Next (toNat (i - 1))

```

要确定一个自然数是偶数还是奇数，我们可以编写一对互相递归的函数：

```
let rec even (n : nat) : bool =
  match n with
    Zero -> true
  | Next n -> odd n
and odd (n : nat) : bool =
  match n with
    Zero -> false
  | Next n -> even n

```

你必须使用关键字 `and` 来组合这样的互相递归函数。否则，在引用尚未定义的 `odd` 时，编译器会报错。

最后，我们可以根据加法来定义乘法。

```
let rec mul (n1 : nat) (n2 : nat) : nat =
  match n1 with
    Zero -> Zero
  | Next m -> add n2 (mul m n2)

```

这给出了

```
toInt (mul (toNat 5) (toNat 20));;
*- : int = 100*

```

* * *

## 模式匹配

结果显示 OCaml 模式的语法比我们在上一堂课上看到的更丰富。除了用于创建和投影元组和记录值，创建和检查变体类型值的新术语外，我们还有能力将模式与值匹配，将其拆分为其部分。

使用正确的方式，模式匹配会导致简洁清晰的代码。这是因为 OCaml 模式匹配允许一个模式出现为另一个模式的子表达式。例如，我们在上面看到 `Next n` 是一个模式，但 `Next (Next n)` 也是一个模式。这第二个模式仅匹配形式为 `Next (Next` *v*`)` 的值，其中 *v* 是某个值（也就是某个东西的后继的后继），并将变量 `n` 绑定到该值。

同样，在我们之前的 `nth` 函数的实现中，一个巧妙的技巧是使用模式匹配来同时执行 `if n = 0` 和 `match`。我们对元组 `(lst, n)` 进行模式匹配：

```
(* Returns the nth element of lst *)
let rec nth lst n =
  match (lst, n) with
    (h :: t, 0) -> h
  | (h :: t, _) -> nth (t, n - 1)
  | ([], _) -> failwith "nth applied to empty list"

```

在这里，我们还添加了一个子句来捕获空列表并引发异常。我们还使用通配符模式 `_` 来匹配元组的 `n` 组件，因为我们不需要将 `n` 的值绑定到另一个变量上——我们已经有了 `n`。我们可以让这段代码变得更短；你能看出来怎么做吗？

### 示例：对记录进行模式匹配

所有的自然数都是非负数，但我们可以通过使用由符号和幅度组成的表示来模拟整数的自然数：

```
type sign = Pos | Neg
type integer = { sign : sign; mag : nat }

```

我们将*整数*定义为具有两个字段：*sign* 和 *mag* 的记录类型。请记住，记录是无序的，因此没有“第一个”字段的概念。

`sign` 和 `integer` 的声明都创建了新类型。然而，可以编写类型声明，只是为现有类型引入新名称。例如，如果我们写了 `type number = int`，那么类型 `number` 和 `int` 可以互换使用。

我们可以使用`integer`的定义来写一些整数：

```
let zero   = {sign=Pos; mag=Zero}
let zero'  = {sign=Neg; mag=Zero}
let one    = {sign=Pos; mag=Next Zero}
let negOne = {sign=Neg; mag=Next Zero}

```

现在我们可以编写一个函数来确定任意整数的后继：

```
let inc (i : integer) : integer =
  match i with
    {sign = _; mag = Zero} -> {sign = Pos; mag = Next Zero}
  | {sign = Pos; mag = n} -> {sign = Pos; mag = Next n}
  | {sign = Neg; mag = Next n} -> {sign = Neg; mag = n}

```

在这里，我们正在模式匹配记录类型。请注意，在第三个模式中，我们进行模式匹配，因为`mag`字段与一个模式本身匹配，`Next n`。请记住，模式是按顺序测试的。如果前两个模式被交换，这个函数的含义会发生什么变化？

前驱函数非常相似，很明显我们可以编写函数来在这种表示中添加、减去和乘以整数。

* * *

### OCaml 语法

考虑到编写复杂模式的能力，我们现在可以为 OCaml 编写一个更全面的语法了。

| 语法类 | 语法变量和语法规则 | 示例 |
| --- | --- | --- |
| **标识符** | *x*,  *y* | `a`, `x`, `y`, `x_y`, `foo1000`, ... |
| **数据类型，数据类型构造函数** | *X*, *Y* | `Nil`, `Cons`, `list` |

| **常量** | *c* | ...`~2`, `~1`, `0`, `1`, `2`（整数） `1.0`, `~0.001`, `3.141`（浮点数）

`true`, `false`（布尔值）

`"hello"`, `""`, `"!"`（字符串）

`#"A"`, `#" "`（字符） |

| **一元运算符** | *u* | `~`, `not`, `size`, ... |
| --- | --- | --- |
| **二进制运算符** | *b* | `+`, `*`, `-`, `>`, `<`, `>=`, `<=`, `^`, ... |
| **表达式（项）** | *e* ::-  *c * &#124; *x*  &#124;  *u e * &#124;  *e*[1]* b e*[2] *&#124;*`if`* e*[1]`then`* e*[2] `else`* e*[3]*  &#124;  *`let` *d*[1]*...**d[n]*`in`*e*`end`  &#124;  *e* `(`*e*[1]`,` ...`,` *e[n]*`)`  `&#124; (`*e*[1]`,`...`,`*e[n]*`)`  `&#124; #`*n e*  &#124;   `{`*x*[1]`=`*e*[1]`,` ...`,` *x[n]*`=`*e[n]*`}`  `&#124; #`*x e  *&#124;   *X*`(`*e*`)`  &#124;  `match`* e *`with `*p*[1]`->`*e*[1]&#124; ... &#124; *p[n]*`->`*e[n]* | `~0.001`, `foo`, `not` `b`, `2 + 2`, `Cons(2, Nil)` |
| **模式** | *p* ::= *c * &#124; *x*  &#124;  `(`*p*[1]`,`...`,` *p[n]*`)`*  &#124;  *`{`*x*[1]`=` *p*[1]`,`...`,`*x[n]*`=`*p[n]*`}`  &#124;  *X*  &#124;  *X* `(` *p* `)` | `a:int`, `(x:int,y:int), I(x:int)` |

| **声明** | *d* ::= `val`*p* *=* *e  *&#124;`fun`* y p *`:` *t* - *e*  &#124;  `da``tatype` *Y*- *X*[1]**[**`of `*t*[1]**]** `&#124;` ... `&#124;` *X[n ]***[**`of` *t**[n]***]** | `val one = 1 fun square(x: int):  int

datatype d - N &#124; I of int` |

| **类型** | *t* ::= `int`  &#124;  `float`  &#124;  `bool`  &#124;  `string`  &#124;  `char`  &#124;  *t*[1]`->`*t*[2]*  &#124;  **t*[1]`*`...`*`*t[n]*  &#124;  `{`*x*[1]:*t*[1], *x*[2]:*t*[2],..., *x[n]*:*t[n]*`}`  &#124;  *Y* | `int`, `string`, `int->int`, `bool*int->bool` |
| --- | --- | --- |
| **values** | *v* ::= *c*  &#124;  `(`*v*[1]`,`...`,`*v[n]*`)` &#124;  `{`*x*[1]`=`*v*[1]`,` ...`,` *x[n]*`=`*v[n]*`}`  &#124;  *X*`(`*v*`)` | `2`, `(2,"hello")`, `Cons(2,Nil)` |

*注意：*对浮点常量进行模式匹配是不明智的。浮点数的相等性是一件危险的事情。最好的做法是测试一个浮点数是否在另一个浮点数的某个小距离 epsilon 内。

## 多态性

有一个很好的功能，可以让我们避免一遍又一遍地重写相同的代码，以便它适用于不同的类型。假设我们想要编写一个函数，用于交换有序对中值的位置：

```
let swapInt ((x : int), (y : int)) : int * int = (y, x)
and swapReal ((x : float), (y : float)) : float * float = (y, x)
and swapString ((x : string), (y : string)) : string * string = (y, x)

```

这很烦人，因为我们每次都要写完全相同的算法。更糟糕的是！如果两个对元素具有不同的类型怎么办？

```
let swapIntReal ((x : int), (y : float)) : float * int = (y, x)
and swapRealInt ((x : float), (y : int)) : int * float = (y, x)

```

等等。有一种更好的方法：

```
# let swap ((x : 'a), (y : 'b)) : 'b * 'a = (y, x);;
*val swap : 'a * 'b -> 'b * 'a = <fun>*

```

我们不是为 `x` 和 `y` 写明确的类型，而是写下**类型变量**`'a` 和 `'b`。`swap` 的类型是 `'a * 'b` `->` `'b * 'a`*.* 这意味着我们可以将 swap 视为具有任何我们能够通过*一致*替换其类型中的`'a`和`'b`而获得的类型。我们可以将新的 swap 替换为所有旧定义：

```
swap (1, 2)         (* (int * int) -> (int * int) *)
swap (3.14, 2.17)   (* (float * float) -> (float * float) *)
swap ("foo", "bar") (* (string * string) -> (string * string) *)
swap ("foo", 3.14)  (* (string * float) -> (float * string) *)

```

实际上，我们可以在 `swap` 的定义中省略类型声明，OCaml 将自动找出它可以给出的最一般的多态类型：

```
# let swap (x, y) = (y, x);;
*val swap : 'a * 'b -> 'b * 'a = <fun>*

```

将 `swap` 用作具有许多不同类型的能力被称为**多态性**，源自希腊语的 "多种形式"。

请注意，在任何多态表达式的使用中，类型变量必须一致地进行替换。例如，`swap`的类型为`(int * float) -> (string * int)`是不可能的，因为该类型会一致替换类型变量`'a`，但不会替换`'b`。

OCaml 程序员通常将类型`'a`和`'b`读作 "alpha" 和 "beta"。这比说 "单引号 a" 或 "撇号 a" 要容易。他们还希望能够使用希腊字母。类型变量可以是由单引号引导的任何标识符；例如，`'key` 和 `'value` 也是合法的类型变量。OCaml 编译器需要在这些标识符之前加上单引号，以便知道它正在看到一个类型变量。

重要的是要注意，要使参数 `x` 在多态上成立，函数不能以任何方式使用 `x` 以确定其类型。它必须将 `x` 视为黑盒子。请注意，`swap` 并不以任何有趣的方式使用其参数 `x` 或 `y`，而是将它们视为黑盒子。当 OCaml 类型检查器检查 `swap` 的定义时，它只知道 `x` 是某种任意类型 `'a`。它不允许对 `x` 执行任何不能在任意类型上执行的操作。这意味着该代码保证适用于任何 `x` 和 `y`。然而，我们可以应用其他多态函数。例如，

```
# let appendToString ((x : 'a), (s : string), (convert : 'a -> string)) : string =
    (convert x) ^ " " ^ s;;
*val appendToString : 'a * string * ('a -> string) -> string = <fun>*
# appendToString (3110, "class", string_of_int);;
*- : string = "3110 class"*
# appendToString ("ten", "twelve", fun (s : string) -> s ^ " past");;
*- : string = "ten past twelve"*

```

* * *

## 参数化类型

我们还可以定义多态数据类型。例如，我们将整数列表定义为

```
type intList = Nil | Cons of (int * intList)
```

但是，我们可以通过使用**参数化变体类型**来使其更通用：

```
type 'a list_ = Nil | Cons of ('a * 'a list_)
```

参数化数据类型是创建一系列相关数据类型的方法。名称`'a`是**类型参数**，可以提供任何其他类型。例如，`int list_`是整数列表，`float list_`是浮点数列表，依此类推。但是，`list_`本身并不是一种类型。还要注意，我们不能使用`list_`来创建每个元素可以是任何类型的列表。`T list_`的所有元素必须是`T`。

```
let il : int list_ = Cons (1, Cons (2, Cons (3, Nil)))  (* [1; 2; 3] *)
let fl : float list_ = Cons (3.14, Cons (2.17, Nil))    (* [3.14; 2.17] *)
let sl : string list_ = Cons ("foo", Cons ("bar", Nil)) (* ["foo"; "bar"] *)
let sil : (string * int) list_ =
  Cons (("foo", 1), Cons (("bar", 2), Nil))  (* [("foo", 1); ("bar", 2)] *)

```

注意`list_`本身并*不是*一种类型。我们可以将`list_`视为一个函数，当应用于类似`int`的类型时，会产生另一种类型（`int` `list_`）。它是一个**参数化类型构造器**：一个接受参数并返回类型的函数。其他语言也有参数化类型构造器。例如，在 Java 中，您可以声明一个**参数化类**：

```
class List<T> {
    T head;
    List <T> tail;
    ...
}

```

在 OCaml 中，我们可以定义**多态函数**，它们知道如何操作任何类型的列表：

```
(* polymorphic lists *)

type 'a list_ = Nil | Cons of 'a * 'a list_

(* is the list empty? *)
let is_empty (lst : 'a list_) : bool = 
  match lst with
  | Nil-> true
  | _ -> false

(* length of the list *)
let rec length (lst : 'a list_) : int = 
  match lst with
  | Nil-> 0
  | Cons (_, rest) -> 1 + length rest

(* append [a; b; c] [d; e; f] = [a; b; c; d; e; f] *)
let rec append (x : 'a list_) (y : 'a list_) : 'a list_ = 
  match x with
  | Nil-> y
  | Cons (h, t) -> Cons (h, append t y)

(* [1; 2; 3] *)
let il = Cons (1, Cons (2, Cons (3, Nil)))
let il2 = append il il
let il4 = append il2 il2
let il8 = append il4 il4
(* ["a"; "b"; "c"] *)
let sl = Cons ("a", Cons ("b", Cons ("c", Nil)))
let sl2 = append sl sl
let sl4 = append sl2 sl2

(* reverse the list:  reverse [1; 2; 3; 4] = [4; 3; 2; 1] *)
let rec reverse (x : 'a list_) : 'a list_ = 
  match x with
  | Nil-> Nil
  | Cons (h, t) -> append (reverse t) (Cons (h, Nil))

let il4r = reverse il4
let sl4r = reverse sl4

(* apply the function f to each element of x
 * map f [a; b; c] = [f a; f b; f c] *)
let rec map (f : 'a -> 'b) (x : 'a list_) : 'b list_ = 
  match x with
  | Nil-> Nil
  | Cons (h, t) -> Cons (f h, map f t)

let mil4 = map string_of_int il4

(* insert sep between each element of x: 
 * separate s [a; b; c; d] = [a; s; b; s; c; s; d] *)
let rec separate (sep : 'a) (x : 'a list_) : 'a list_ = 
  match x with
  | Nil-> Nil
  | Cons (h, Nil) -> x
  | Cons (h, t) -> Cons (h, Cons (sep, separate sep t))

let s0il4 = separate 0 il4

```

对于树，

```
type 'a tree = Leaf | Node of ('a tree) * 'a * ('a tree)

```

如果我们使用记录类型作为节点，那么记录类型也必须是参数化的，并且在树类型的相同元素类型上进行实例化：

```
type 'a tree = Leaf | Node of 'a node
and 'a node = {left: 'a tree; value: 'a; right: 'a tree}

```

还可以在参数化类型上具有多个类型参数，这种情况下需要使用括号：

```
type ('a, 'b) pair = {first: 'a; second: 'b};;
let x = {first=2; second="hello"};;
*val x: (int, string) pair = {first = 2; second = "hello"}*

```

* * *

## 抽象语法和变体类型

我们早些时候注意到，BNF 声明与变体类型声明之间存在相似性。实际上，我们可以定义行为类似于对应的 BNF 声明的变体类型。然后，这些变体类型的值代表了语言中可以出现的合法表达式。例如，考虑一个 OCaml 类型表达式的合法 BNF 定义：

| （基本类型） | *b* ::= `int` &#124; `float` &#124; `string` &#124; `bool` &#124; `char` |
| --- | --- |
| （类型） | *t* ::= *b* &#124; *t* `->` *t* &#124; *t*[1] `*` *t*[2] `*`...`*` *t[n]* &#124; `{` *x*[1] `:` *t*[1]`;` ...`;` *x[n]* `:` *t[n]* `}` &#124; *X* |

这个语法与以下类型声明具有完全相同的结构：

```
type id = string
type baseType = Int | Real | String | Bool | Char
type mlType = Base of baseType | Arrow of mlType * mlType
              | Product of mlType list | Record of (id * mlType) list
              | DatatypeName of id

```

任何合法的 OCaml 类型表达式都可以由包含相应类型表达式所有信息的`mlType`类型的值表示。这个值被称为该表达式的*抽象语法*。它是抽象的，因为它不包含关于在程序中表示表达式的实际符号的任何信息。例如，表达式`int * bool -> {name : string}`的抽象语法将是：

```
Arrow (Product (Cons (Base Int, Cons (Base Bool, Nil))),
       Record (Cons (("name", Base String), Nil)))

```

即使对于相同类型表达式的更详细的版本，抽象语法也会完全相同：`((int * bool) -> {name : string})`。编译器通常在内部使用抽象语法来表示它们正在编译的程序。当我们了解 OCaml 的工作原理时，我们将在课程后期看到更多的抽象语法。
