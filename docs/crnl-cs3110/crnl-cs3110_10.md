# 第 5 次复习：折叠和尾递归

## 折叠

假设我们想要编写一个函数来对整数列表求和。到现在为止，你应该能够编写以下代码：

```
let rec sum (l : int list) : int =
  match l with
    [] -> 0
  | x :: xs -> x + (sum xs)

```

现在假设我们想要连接一个字符串列表。我们可以编写：

```
let rec concat (l : string list) : string =
  match l with
    [] -> ""
  | x :: xs -> x ^ (concat xs)

```

注意这两个函数看起来几乎完全相同。除了不同的类型和不同的操作（^ vs +）之外，这两个函数是等价的。在这两种情况下，我们都遍历列表，在每一步中执行一些操作。由于我们喜欢在这门课上重用代码，是否有一种更简单的方法来编码这个？

结果我们可以将遍历列表的所有细节抽象出来。这个想法很简单。当我们遍历列表时，我们存储一个*累加器*，它存储对我们很重要的一些数据。例如，如果我们想要遍历一个整数列表并对它们求和，我们可以将当前的总和存储在累加器中。我们从累加器设置为 0 开始。当我们遇到每个新元素时，我们将元素添加到累加器中。当我们到达末尾时，我们返回累加器中存储的值。

让我们试着重写求和函数来引入累加器的概念。

```
let rec sum' (acc : int) (l : int list) : int =
  match l with
    [] -> acc
  | x :: xs -> sum' (acc + x) xs

```

当然，为了得到总和，我们必须使用初始值为`0`调用`sum'`来计算累加器。类似地，我们可以使用累加器的概念重写`concat`。

```
let rec concat' (acc : string) (l : string list) : string =
  match l with
    [] -> acc
  | x :: xs -> concat' (acc ^ x) xs

```

要使用此函数，我们将空字符串作为初始累加器传递进去。现在我们看到了两个函数之间更多的相似性。我们可以通过传入作用于列表头部和累加器的运算符来消除两者之间的任何差异。结果就是列表库中最强大的列表函数：`List.fold_left`。

```
let rec fold_left (f : 'a -> 'b ->'a) (acc : 'a) (l : 'b list): 'a =
  match l with
    [] -> acc
  | x :: xs -> fold_left f (f acc x) xs

```

现在我们可以重写`sum`和`concat`如下：

```
let sum (l : int list) : int =
  List.fold_left (fun acc x -> acc + x) 0 l
let concat (l : string list) : string =
  List.fold_left (fun acc x -> acc ^ x) "" l

```

给定类型为`'a -> 'b -> 'a`的函数`f`，表达式`List.fold_left f b [x1; x2; ...; xn]`的求值结果为`f (... (f (f b x1) x2) ...) xn`。`List.fold_left`中的'left'来自于它从左到右遍历列表。因此，我们有时在应用函数时必须注意顺序（因此在上面的`concat`中是`acc ^ x`而不是`x ^ acc`）。还有一个库函数`List.fold_right`，它从右到左遍历列表。请注意，`List.fold_right`的参数顺序与`List.fold_left`不同。列表和累加器参数交换了位置，并且函数接受累加器作为其第二个柯里化参数，而不是第一个。因此，`List.fold_right f [x1; x2; ...; xn] b`的求值结果为`f x1 (f x2 (... (f xn b)...))`。`List.fold_right`的代码为

```
let rec fold_right (f : 'a -> 'b -> 'b) (l : 'a list) (acc : 'b) : 'b =
  match l with
    [] -> acc
  | x :: xs -> f x (List.fold_right f xs acc)

```

我们可以使用`List.fold_right`以更自然的方式编写`concat`。

```
let concat (l : string list) : string =
  List.fold_right (fun x acc -> x ^ acc) l ""

```

但是我们仍然可以做得更好。请记住，`List.fold_left`和`List.fold_right`都是柯里化的，因此我们可以省略列表参数并写成

```
let sum = List.fold_left (fun a x -> x + a) 0
let concat = List.fold_left (fun a x -> a ^ x) ""

```

我们甚至可以做得更好。OCaml 提供了一种便捷的表示法，通过将中缀二进制运算符括在括号中，可以将它们作为柯里化函数使用。因此，`(+)`是一个柯里化函数，接受两个整数并将它们相加，`(^)`是一个柯里化函数，接受两个字符串并将它们连接起来。我们可以最后一次编写 sum 和 concat 如下：

```
let sum = List.fold_left (+) 0
let concat = List.fold_left (^) ""

```

现在你看到了函数式编程的真正威力。尝试用 2 行 Java 代码重写对整数列表求和和对字符串列表进行连接的函数。

折叠是如此强大，以至于我们几乎可以用`List.fold_left`或`List.fold_right`来编写每一个通用的列表函数。例如，

```
let length l = List.fold_left (fun a _ -> a + 1) 0 l
let rev l = List.fold_left (fun a x -> x :: a) [] l
let map f l = List.fold_right (fun x a -> (f x) :: a) l []
let app f l = List.fold_left (fun _ x -> f x) () l
let filter f l =
  List.fold_right (fun x a -> if f x then x :: a else a) l []

```

在使用 fold 编写代码时，OCaml 的类型系统可以极大地帮助。`List.fold_left`的类型是`('a -> 'b -> 'a) -> 'a -> 'b list -> 'a`。假设我们知道我们将`List.fold_left`应用于`int list`并且想要返回一个`string`，那么`'b`的类型是`int`，`'a`的类型是`string`。因此，我们立即知道我们必须传递给`List.fold_left`的函数的类型是`string -> int -> string`。记得要利用你的类型系统来帮助你编写代码。

你应该尝试使用`List.fold_left`编写一些函数，以便更好地了解它。它可以非常有用。

## 尾递归

如果一个函数在递归调用自身后不执行任何计算，而是立即返回其递归调用的值并将其值立即返回给调用者，则称该函数为*尾递归*。观察上面`sum`和`sum'`函数之间的以下区别：在第一个不是尾递归的`sum`函数中，在递归调用返回其值后，我们将`x`添加到它。而在尾递归`sum'`中，在递归调用返回后，我们立即返回该值，不进行进一步的计算。注意，`List.fold_left`是尾递归的，而`List.fold_right`不是。

我们为什么关心尾递归？因为性能。从概念上讲，有一个*调用栈*，它是一个栈（具有推送和弹出操作的数据结构），每个已启动但尚未完成的函数调用都有一个元素。每个元素都存储着诸如局部变量的值以及尚未评估的函数部分。当一个函数的评估调用另一个函数时，将在调用栈上推送一个新元素，当被调用的函数完成时，它就会被弹出。当一个函数对自身进行递归调用，并且在调用方在被调用方返回后*没有其他事情要做*（除了返回被调用方的结果）时，这种情况被称为*尾调用*。像 OCaml（甚至像 C++这样的命令式语言）这样的函数式语言通常包含一个非常有用的优化：当一个调用是尾调用时，调用者的栈帧在调用前被弹出—被调用者的栈帧就替换了调用者的栈帧。这是有道理的：调用者只是要返回被调用者的结果。通过这种优化，递归有时可以像命令式语言中的`while`循环一样高效（这样的循环不会使调用栈变大）。"有时候"指的正是调用是尾调用的时候——你和编译器通常可以（经常）弄清楚。通过尾调用优化，递归算法的空间性能可以从 O(n)降低到 O(1)，也就是说，从每次调用一个栈帧到所有调用只使用一个栈帧。

当你在使用`List.fold_left`和`List.fold_right`之间做选择时，通常最好在非常长的列表上使用`List.fold_left`。然而，有时候用`List.fold_right`来表达一个计算会更容易（注意我们在关于 map 和 fold 的讲座中是如何用`fold_right`来写`map`的），因此在性能和代码清晰度之间存在权衡。如果你想在一个非常长的列表上使用`List.fold_right`，你可能会选择先反转列表，然后再使用`List.fold_left`。例如，我们可以将 map 写成以下形式：

```
let map f l = List.fold_left (fun a x -> (f x) :: a) [] (List.rev l)

```

这导致了一个尾递归，从而实现了高效的空间利用。但这并不意味着尾递归实现就一定更好。对于小到中等大小的列表，反转列表的开销（无论是时间上还是为反转列表分配内存）可能会使尾递归版本的效率*降低*。
